10 CLEAR
20 REM *** PROGRAM BY CHRISTOPHER D. VAN DER KAAY, PH.D. 2025 ***
30 REM *** SIMPLE EXPONENTIAL SMOOTHING FORECASTING TOOL V1.2 FOR MBASIC-80 ***
40 REM *** REM - UP TO 100 DATA POINTS; FORECAST HORIZON N+H â‰¤ 100 ***
50 REM *** OPTIMIZES SMOOTHING PARAMETER ALPHA BY BRUTE-FORCE SEARCH ***
60 REM *** COMPUTES AND DISPLAYS STATISTICAL ACCURACY MEASURES ***
70 PRINT STRING$(79, "*")
80 PRINT TAB(5); "SIMPLE EXPONENTIAL SMOOTHING METHOD FOR TIME-SERIES FORECASTING V1.2"
90 PRINT STRING$(79, "*")
100 PRINT
110 PRINT TAB(5); "THIS PROGRAM USES THE SIMPLE EXPONENTIAL SMOOTHING METHOD"
120 PRINT TAB(5); "TO FORECAST FUTURE VALUES IN A TIME SERIES BASED ON HISTORICAL"
130 PRINT TAB(5); "DATA POINTS. IT CALCULATES FORECASTED VALUES BY AUTOMATICALLY"
140 PRINT TAB(5); "DETERMINING THE OPTIMAL ALPHA VALUE THAT MINIMIZES THE FORECAST ERROR"
150 PRINT TAB(5); "AND PROVIDES VARIOUS STATISTICAL MEASURES OF FORECAST ACCURACY."
160 PRINT TAB(5); "THIS METHOD IS USEFUL FOR SERIES WITH NO TREND OR SEASONAL PATTERN."
170 PRINT
180 PROMPT$ = "LEARN ABOUT THE FORECAST ACCURACY METRICS BEFORE PROCEEDING? (Y/N): "
190 GOSUB 3630
200 IF RESP$ = "Y" THEN GOSUB 1790
210 PRINT
220 PRINT "ENTER THE HISTORICAL DATA POINTS AND FORECAST HORIZON."
230 PRINT "THE PROGRAM WILL AUTOMATICALLY CALCULATE THE OPTIMAL ALPHA VALUE"
240 PRINT "THAT MINIMIZES THE FORECAST ERROR."
250 PRINT
260 DIM Y(100), F(100), S(100)   ' Y() = INPUT DATA; F() = FORECASTS; S() = SMOOTHED VALUES
270 DIM U(100), L(100)  ' U() AND L() FOR UPPER/LOWER CONFIDENCE INTERVALS; ALL ARRAYS INDEX 1..N+H
280 INPUT "ENTER NUMBER OF HISTORICAL DATA POINTS: "; N
290 IF N < 2 OR N > 100 THEN PRINT "INVALID INPUT. ENTER AN INTEGER BETWEEN 2 AND 100.": GOTO 280
300 AVE = 0
310 FOR I = 1 TO N
320 INPUT "ENTER DATA POINT "; A$   ' ACCEPT INPUT AS STRING TO VALIDATE NUMERICS
330 Y(I) = VAL(A$)   ' CONVERT TO NUMERIC VALUE
340 IF Y(I)=0 THEN IF INSTR(A$,"0")=0 THEN PRINT "INVALID INPUT. ENTER A NUMERICAL VALUE.": GOTO 320
350 AVE = AVE + Y(I)
360 NEXT I
370 PRINT
380 GOSUB 3730 ' *** EXPLANATORY MESSAGE ABOUT STATIONARITY TEST ***
390 PROMPT$ = "VIEW THE FIRST-DIFFERENCED SERIES FOR STATIONARITY CHECK? (Y/N): "
400 GOSUB 3630
410 IF RESP$ = "Y" THEN GOSUB 2650
420 PROMPT$ = "COMPARE MEANS AND VARIANCES OF THE FIRST AND SECOND HALVES? (Y/N): "
430 GOSUB 3630
440 IF RESP$ = "Y" THEN GOSUB 2840
450 AVE = AVE / N
460 PRINT
470 INPUT "ENTER FORECAST HORIZON: "; H
480 IF H <= 0 THEN PRINT "INVALID INPUT. ENTER A POSITIVE INTEGER.": GOTO 470
490 IF N + H > 100 THEN PRINT "N + H EXCEEDS 100. REDUCE FORECAST HORIZON.": GOTO 470
500 PRINT
510 INPUT "ENTER STEP SIZE (BETWEEN 0.001 AND 0.1): "; ALPHASTEP
520 IF ALPHASTEP < .001 OR ALPHASTEP > .1 THEN PRINT "INVALID INPUT. ENTER A VALUE BETWEEN 0.001 AND 0.1.": GOTO 510
530 PRINT "CALCULATING OPTIMAL ALPHA VALUE..."
540 PRINT "USING STEP SIZE OF "; ALPHASTEP; " FOR ALPHA SEARCH..."
550 TSS = 0
560 FOR I = 1 TO N
570 TSS = TSS + (Y(I) - AVE) ^ 2
580 NEXT I
590 DIM TEMPS(100), TEMPF(100)
600 BALPHA = 0
610 MINMSE = 1E+30
620 FOR ALPHA = ALPHASTEP TO .99 STEP ALPHASTEP  ' BRUTE-FORCE GRID SEARCH FOR ALPHA IN [ALPHASTEP, 0.99]
630 TEMPS(1) = Y(1)
640 TEMPF(1) = Y(1)
650 SSE = 0
660 FOR I = 2 TO N   ' LOOP OVER DATA TO COMPUTE MSE FOR CURRENT ALPHA
670 TEMPS(I) = ALPHA * Y(I) + (1 - ALPHA) * TEMPS(I-1)
680 TEMPF(I) = TEMPS(I-1)
690 SSE = SSE + (Y(I) - TEMPF(I)) ^ 2
700 NEXT I
710 MSE = SSE / (N - 1)
720 PRINT "ALPHA = "; ALPHA; " MSE = "; MSE  'PRINT ALPHA AND MSE
730 IF MSE < MINMSE THEN GOTO 750    ' KEEP MINIMUM MSE AND CORRESPONDING ALPHA
740 GOTO 770
750 MINMSE = MSE
760 BALPHA = ALPHA
770 NEXT ALPHA
780 ALPHA = BALPHA
790 PRINT CHR$(26):PRINT "OPTIMAL ALPHA VALUE IS: "; ALPHA
800 PRINT
810 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
820 PRINT
830 S(1) = Y(1)
840 F(1) = Y(1)
850 SSE = 0
860 MAPE = 0
870 MAPECOUNT = 0
880 FOR I = 2 TO N
890 S(I) = ALPHA * Y(I) + (1 - ALPHA) * S(I-1)   ' COMPUTE SMOOTHED VALUE FOR PERIOD I
900 F(I) = S(I-1)   ' FORECAST FOR I IS PREVIOUS SMOOTHED VALUE
910 SSE = SSE + (Y(I) - F(I)) ^ 2
920 IF Y(I) <> 0 THEN GOTO 940   ' ONLY CALCULATE MAPE IF ACTUAL DOES NOT EQUAL 0 TO AVOID DIVISION BY ZERO
930 GOTO 960
940 MAPE = MAPE + ABS((Y(I) - F(I)) / Y(I))
950 MAPECOUNT = MAPECOUNT + 1
960 NEXT I
970 F(N+1) = S(N)
980 FOR I = 2 TO H
990 F(N+I) = S(N)
1000 NEXT I
1010 REM (S() BEYOND N IS NOT NEEDED FOR SIMPLE EXP SMOOTHING FORECASTS)
1020 REM (INTENTIONALLY BLANK)
1030 REM *** COMPUTE FORECAST INTERVALS ***
1040 GOSUB 3460
1050 MSE = SSE / (N - 1)
1060 MAE = 0
1070 COUNT = 0
1080 FOR I = 2 TO N
1090 MAE = MAE + ABS(Y(I) - F(I))
1100 COUNT = COUNT + 1
1110 NEXT I
1120 MAE = MAE / COUNT
1130 IF MAPECOUNT > 0 THEN MAPE = MAPE / MAPECOUNT * 100 ELSE MAPE = -1
1140 SMAPE = 0
1150 SMAPECOUNT = 0
1160 FOR I = 2 TO N
1170 NUM = ABS(Y(I) - F(I))
1180 DEN = ABS(Y(I)) + ABS(F(I))
1190 IF DEN = 0 THEN GOTO 1220   ' PREVENT DIVISION BY ZERO IF BOTH ACTUAL AND FORECAST ARE ZERO
1200 SMAPE = SMAPE + (NUM / (DEN / 2))
1210 SMAPECOUNT = SMAPECOUNT + 1
1220 NEXT I
1230 IF SMAPECOUNT > 0 THEN SMAPE = 100 * SMAPE / SMAPECOUNT ELSE SMAPE = -1
1240 PRINT CHR$(7):PRINT "FORECASTING RESULTS:"
1250 PRINT "*************************************************************"
1260 LINES = 0
1270 FOR I = 1 TO H
1280 PRINT "PERIOD "; I; " ";
1290 PRINT " : FORECAST = " F(N+I)
1300 PRINT "   LOWER 95% = " L(N+I)
1310 PRINT "   UPPER 95% = " U(N+I)
1320 PRINT
1330 LINES = LINES + 4
1340 IF LINES >= 15 THEN INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$: PRINT CHR$(26): LINES = 0   ' PAUSE AFTER 15 LINES
1350 NEXT I
1360 PRINT "*************************************************************"
1370 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
1380 PRINT
1390 PRINT "OPTIMAL ALPHA VALUE: "; ALPHA
1400 PRINT "MEAN ABSOLUTE ERROR (MAE): "; MAE
1410 PRINT "MEAN SQUARED ERROR (MSE): "; MSE
1420 PRINT "ROOT MEAN SQUARED ERROR (RMSE): "; (MSE ^ .5)
1430 IF MAPE >= 0 THEN PRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): "; MAPE; "%" : GOTO 1450
1440 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): N/A"
1450 IF SMAPE >= 0 THEN PRINT "SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR (SMAPE): "; SMAPE; "%" : GOTO 1470
1460 PRINT "SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR (SMAPE): N/A"
1470 PRINT
1480 REM *** DISPLAY MAPE THRESHOLDS AND INTERPRETATION ***
1490 PRINT "MAPE INTERPRETATION THRESHOLDS:"
1500 PRINT "*************************************************************"
1510 PRINT "MAPE < 10%           : HIGHLY ACCURATE FORECASTING"
1520 PRINT "10% <= MAPE < 20%    : GOOD FORECASTING"
1530 PRINT "20% <= MAPE < 50%    : REASONABLE FORECASTING"
1540 PRINT "MAPE >= 50%          : INACCURATE FORECASTING"
1550 PRINT "*************************************************************"
1560 PRINT
1570 REM *** INTERPRETATION OF MAPE ***
1580 IF MAPE < 0 THEN PRINT "INTERPRETATION: N/A (MAPE UNDEFINED DUE TO ZERO ACTUAL VALUES)": GOTO 1670
1590 IF MAPE < 10 THEN GOTO 1660
1600 IF MAPE < 20 THEN GOTO 1650
1610 IF MAPE < 50 THEN GOTO 1640
1620 GOTO 1630
1630 PRINT "INTERPRETATION: INACCURATE FORECASTING" : GOTO 1670
1640 PRINT "INTERPRETATION: REASONABLE FORECASTING" : GOTO 1670
1650 PRINT "INTERPRETATION: GOOD FORECASTING" : GOTO 1670
1660 PRINT "INTERPRETATION: HIGHLY ACCURATE FORECASTING"
1670 PRINT
1680 PRINT "(R)ESTART, (S)HOW RESULTS, (P)RINT REPORT, OR (E)ND"
1690 INPUT "(R/S/P/E): "; R$
1700 IF R$ = "R" THEN GOTO 10
1710 IF R$ = "S" THEN GOTO 1240
1720 IF R$ = "P" THEN PROMPT$ = "===> IS THE PRINTER POWERED ON AND READY? (Y/N): " : GOSUB 3630
1730 IF RESP$ = "Y" THEN GOSUB 3940 : GOTO 1680
1740 IF RESP$ = "N" THEN PRINT "SKIPPING PRINTING.": GOTO 1680
1750 IF R$ = "E" THEN GOTO 1770
1760 PRINT "INVALID INPUT. ENTER R, S, P, OR E.": GOTO 1690
1770 PRINT CHR$(7):PRINT "THANK YOU FOR USING THE TIME-SERIES FORECASTING TOOL!"
1780 END
1790 REM *** EXPLANATION SUBROUTINE ***
1800 PRINT CHR$(26)
1810 PRINT STRING$(79, "*")
1820 PRINT TAB(22); "      FORECAST ACCURACY METRICS"
1830 PRINT STRING$(79, "*")
1840 GOSUB 1900 ' MAE
1850 GOSUB 2060 ' MSE
1860 GOSUB 2210 ' RMSE
1870 GOSUB 2360 ' MAPE
1880 PRINT
1890 RETURN
1900 REM *** MAE (MEAN ABSOLUTE ERROR) EXPLANATION ***
1910 PRINT 
1920 PRINT " MAE (MEAN ABSOLUTE ERROR)"
1930 PRINT STRING$(79, "*")
1940 PRINT " THE MAE MEASURES THE AVERAGE MAGNITUDE OF THE"
1950 PRINT " ERRORS IN A SET OF FORECASTS, WITHOUT CONSIDERING"
1960 PRINT " THEIR DIRECTION." 
1970 PRINT
1980 PRINT " USAGE: MAE IS USED TO UNDERSTAND THE AVERAGE"
1990 PRINT " ERROR MAGNITUDE. LOWER MAE VALUES INDICATE BETTER"
2000 PRINT " ACCURACY."
2010 PRINT
2020 PRINT STRING$(79, "*")
2030 PRINT
2040 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2050 RETURN
2060 REM *** MSE (MEAN SQUARED ERROR) EXPLANATION ***
2070 PRINT 
2080 PRINT " MSE (MEAN SQUARED ERROR)"
2090 PRINT STRING$(79, "*")
2100 PRINT " THE MSE MEASURES THE AVERAGE OF THE SQUARES OF"
2110 PRINT " THE ERRORS."
2120 PRINT
2130 PRINT " USAGE: MSE PENALIZES LARGER ERRORS MORE THAN"
2140 PRINT " SMALLER ONES, MAKING IT USEFUL FOR IDENTIFYING"
2150 PRINT " MODELS THAT NEED TO MINIMIZE LARGE ERRORS."
2160 PRINT
2170 PRINT STRING$(79, "*")
2180 PRINT
2190 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2200 RETURN
2210 REM *** RMSE (ROOT MEAN SQUARED ERROR) EXPLANATION ***
2220 PRINT 
2230 PRINT " RMSE (ROOT MEAN SQUARED ERROR)"
2240 PRINT STRING$(79, "*")
2250 PRINT " THE RMSE IS THE SQUARE ROOT OF THE AVERAGE OF"
2260 PRINT " THE SQUARED ERRORS."
2270 PRINT
2280 PRINT " USAGE: RMSE PROVIDES A MEASURE OF THE SPREAD OF"
2290 PRINT " THE ERRORS IN THE SAME UNITS AS THE DATA, MAKING"
2300 PRINT " IT EASIER TO INTERPRET."
2310 PRINT 
2320 PRINT STRING$(79, "*")
2330 PRINT
2340 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2350 RETURN
2360 REM *** MAPE (MEAN ABSOLUTE PERCENTAGE ERROR) EXPLANATION ***
2370 PRINT 
2380 PRINT " MAPE (MEAN ABSOLUTE PERCENTAGE ERROR)"
2390 PRINT STRING$(79, "*")
2400 PRINT " THE MAPE MEASURES THE AVERAGE MAGNITUDE OF THE"
2410 PRINT " ERRORS AS A PERCENTAGE OF THE ACTUAL VALUES."
2420 PRINT
2430 PRINT " USAGE: MAPE NORMALIZES THE ERRORS AS PERCENTAGES,"
2440 PRINT " FACILITATING COMPARISONS ACROSS DIFFERENT SCALES."
2450 PRINT 
2460 PRINT STRING$(79, "*")
2470 PRINT
2480 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2490 REM *** SMAPE (SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR) EXPLANATION ***
2500 PRINT 
2510 PRINT " SMAPE (SYMMETRIC MEAN ABSOLUTE PERCENTAGE ERROR)"
2520 PRINT STRING$(79, "*")
2530 PRINT " THE SMAPE IS A MODIFICATION OF MAPE THAT IS SYMMETRIC,"
2540 PRINT " MEANING IT TREATS OVER- AND UNDER-FORECASTS EQUALLY AND"
2550 PRINT " HANDLES ZERO OR NEAR-ZERO ACTUAL VALUES BETTER."
2560 PRINT
2570 PRINT " USAGE: SMAPE IS USEFUL FOR TIME SERIES WHERE ACTUALS"
2580 PRINT " CAN BE ZERO OR CLOSE TO ZERO. IT PROVIDES A PERCENTAGE"
2590 PRINT " ERROR THAT IS BOUNDED BETWEEN 0% AND 200%."
2600 PRINT 
2610 PRINT STRING$(79, "*")
2620 PRINT
2630 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2640 RETURN
2650 REM *** FIRST-DIFFERENCING SUBROUTINE WITH PAGING ***
2660 REM *** DISPLAYS THE FIRST-DIFFERENCED TIME SERIES (Y(I) - Y(I-1)), PAUSING EVERY 16 LINES ***
2670 PRINT CHR$(26)
2680 PRINT "FIRST-DIFFERENCED SERIES"
2690 PRINT STRING$(60, "*")
2700 LINES = 0
2710 FOR I = 2 TO N
2720 D = Y(I) - Y(I - 1)
2730 PRINT "DIFF("; I; ") = "; D
2740 LINES = LINES + 1
2750 IF LINES = 16 THEN GOSUB 2800: LINES = 0
2760 NEXT I
2770 PRINT STRING$(60, "*")
2780 IF LINES > 0 THEN GOSUB 2800
2790 RETURN
2800 REM *** PAUSE FOR PAGINATION ***
2810 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
2820 PRINT CHR$(26)
2830 RETURN
2840 REM *** MEAN AND VARIANCE STABILITY CHECK ***
2850 REM *** COMPUTE MIDPOINT OF SERIES ***
2860 MID = INT(N / 2)
2870 SUM1 = 0: SUM2 = 0
2880 FOR I = 1 TO MID
2890 SUM1 = SUM1 + Y(I)
2900 NEXT I
2910 FOR I = MID + 1 TO N
2920 SUM2 = SUM2 + Y(I)
2930 NEXT I
2940 MEAN1 = SUM1 / MID
2950 MEAN2 = SUM2 / (N - MID)
2960 REM *** NOW COMPUTE VARIANCES ***
2970 VAR1 = 0: VAR2 = 0
2980 FOR I = 1 TO MID
2990 VAR1 = VAR1 + (Y(I) - MEAN1) ^ 2
3000 NEXT I
3010 VAR1 = VAR1 / MID
3020 FOR I = MID + 1 TO N
3030 VAR2 = VAR2 + (Y(I) - MEAN2) ^ 2
3040 NEXT I
3050 VAR2 = VAR2 / (N - MID)
3060 REM *** PRINT RESULTS ***
3070 PRINT CHR$(26)
3080 PRINT "STATIONARITY CHECK: MEAN AND VARIANCE COMPARISON"
3090 PRINT STRING$(60, "*")
3100 PRINT "FIRST HALF MEAN     = "; MEAN1
3110 PRINT "SECOND HALF MEAN    = "; MEAN2
3120 PRINT "FIRST HALF VARIANCE = "; VAR1
3130 PRINT "SECOND HALF VARIANCE= "; VAR2
3140 PRINT STRING$(60, "*")
3150 IF ABS(MEAN1) = 0 THEN IF ABS(MEAN2 - MEAN1) > .1 THEN GOTO 3200
3160 IF ABS(MEAN1) <> 0 THEN IF ABS(MEAN2 - MEAN1) > .1 * ABS(MEAN1) THEN GOTO 3200
3170 IF VAR1 = 0 THEN IF VAR2 > 0 THEN GOTO 3200
3180 IF VAR1 <> 0 THEN IF ABS(VAR2 - VAR1) > .2 * VAR1 THEN GOTO 3200   ' WARN IF VARIANCE SHIFT >20%
3190 GOTO 3210
3200 PRINT "WARNING: MEAN/VARIANCE SHIFT DETECTED! POSSIBLE NON-STATIONARITY!"
3210 PRINT
3220 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3230 RETURN
3240 REM *** MAPE (MEAN ABSOLUTE PERCENTAGE ERROR) ***
3250 PRINT STRING$(79, "*")
3260 PRINT " MAPE (MEAN ABSOLUTE PERCENTAGE ERROR)"
3270 PRINT STRING$(79, "*")
3280 PRINT " THE MAPE MEASURES THE AVERAGE MAGNITUDE OF THE"
3290 PRINT " ERRORS AS A PERCENTAGE OF THE ACTUAL VALUES. IT"
3300 PRINT " IS CALCULATED AS:"
3310 PRINT
3320 PRINT "   MAPE = (100/N) * SUM(|(FORECASTI - ACTUALI) / ACTUALI|)"
3330 PRINT
3340 PRINT " WHERE:"
3350 PRINT "   N IS THE NUMBER OF FORECASTS"
3360 PRINT "   FORECASTI IS THE FORECASTED VALUE"
3370 PRINT "   ACTUALI IS THE ACTUAL VALUE"
3380 PRINT
3390 PRINT " USAGE: MAPE NORMALIZES THE ERRORS AS PERCENTAGES,"
3400 PRINT " FACILITATING COMPARISONS ACROSS DIFFERENT SCALES."
3410 PRINT
3420 PRINT STRING$(79, "*")
3430 PRINT
3440 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3450 RETURN
3460 REM *** SUBROUTINE FOR COMPUTING FORECAST INTERVALS ***
3470 REM *** COMPUTE 95% FORECAST INTERVALS (SES, NO TREND/SEASONALITY) ***
3480 RESVAR = SSE / (N - 1)
3490 PHI = 1 - ALPHA
3500 ZVAL = 1.96  ' 95% CONFIDENCE
3510 FOR I = 1 TO H
3520 IF I = 1 THEN VARFACTOR = 1: GOTO 3580
3530 SUM = 0
3540 FOR J = 0 TO I - 2
3550 SUM = SUM + (PHI ^ (2 * J))
3560 NEXT J
3570 VARFACTOR = 1 + (ALPHA * ALPHA) * SUM
3580 STDERR = SQR(RESVAR * VARFACTOR)
3590 U(N+I) = F(N+I) + ZVAL * STDERR
3600 L(N+I) = F(N+I) - ZVAL * STDERR
3610 NEXT I
3620 RETURN
3630 REM *** Y/N VALIDATION SUBROUTINE ***
3640 REM *** PROMPTS USER WITH PROMPT$, ACCEPTS ONLY Y/N (CASE-INSENSITIVE), REPEATS UNTIL VALID ***
3650 PRINT
3660 PRINT PROMPT$;
3670 INPUT ""; RESP$
3680 IF RESP$ = "" THEN PRINT "ENTER Y OR N.": GOTO 3660
3690 RESP$ = LEFT$(RESP$,1)
3700 IF RESP$="Y" OR RESP$="y" THEN RESP$="Y": RETURN
3710 IF RESP$="N" OR RESP$="n" THEN RESP$="N": RETURN
3720 PRINT "INVALID INPUT. ENTER Y OR N.": GOTO 3660
3730 REM *** STATIONARITY CHECK EXPLANATION SUBROUTINE ***
3740 PRINT CHR$(26)
3750 PRINT STRING$(79, "*")
3760 PRINT "STATIONARITY CHECK EXPLANATION"
3770 PRINT STRING$(79, "*")
3780 PRINT "THE PROGRAM OPTIONALLY CHECKS FOR STATIONARITY BY COMPARING"
3790 PRINT "THE MEAN AND VARIANCE OF THE FIRST HALF AND SECOND HALF OF"
3800 PRINT "THE INPUT TIME SERIES."
3810 PRINT
3820 PRINT "A WARNING WILL BE ISSUED IF EITHER OF THE FOLLOWING IS TRUE:"
3830 PRINT " - THE ABSOLUTE DIFFERENCE IN MEANS EXCEEDS 10% OF THE"
3840 PRINT "   FIRST HALF'S MEAN."
3850 PRINT " - OR THE ABSOLUTE DIFFERENCE IN VARIANCES EXCEEDS 20% OF"
3860 PRINT "   THE FIRST HALF'S VARIANCE."
3870 PRINT
3880 PRINT "THIS INDICATES A SHIFT IN LEVEL OR SPREAD, SUGGESTING"
3890 PRINT "THE DATA MAY BE NON-STATIONARY, WHICH CAN REDUCE THE"
3900 PRINT "ACCURACY OF SIMPLE EXPONENTIAL SMOOTHING FORECASTS."
3910 PRINT STRING$(79, "*")
3920 INPUT "PRESS RETURN TO CONTINUE..."; DUMMY$
3930 RETURN
3940 REM *** STATISTICAL REPORT ***
3950 LPRINT STRING$(79, "*")
3960 LPRINT TAB(22); "STATISTICAL SUMMARY REPORT"
3970 LPRINT STRING$(79, "*")
3980 LPRINT "INPUT DATA POINTS: "; N; TAB(36); "FORECAST HORIZON: "; H
3990 LPRINT
4000 LPRINT "OPTIMAL ALPHA: "; ALPHA
4010 LPRINT "MEAN ABSOLUTE ERROR (MAE): "; MAE
4020 LPRINT "MEAN SQUARED ERROR (MSE): "; MSE
4030 LPRINT "ROOT MEAN SQUARED ERROR (RMSE): "; (MSE ^ .5)
4040 IF MAPE >= 0 THEN LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): "; MAPE; "%" : GOTO 4060
4050 LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR (MAPE): N/A"
4060 IF SMAPE >= 0 THEN LPRINT "SYMMETRIC MAPE (SMAPE): "; SMAPE; "%" : GOTO 4080
4070 LPRINT "SYMMETRIC MAPE (SMAPE): N/A"
4080 LPRINT
4090 LPRINT "MAPE INTERPRETATION:"
4100 IF MAPE < 0 THEN LPRINT "   N/A" : GOTO 4150
4110 IF MAPE < 10 THEN LPRINT "   HIGHLY ACCURATE FORECASTING" : GOTO 4150
4120 IF MAPE < 20 THEN LPRINT "   GOOD FORECASTING" : GOTO 4150
4130 IF MAPE < 50 THEN LPRINT "   REASONABLE FORECASTING" : GOTO 4150
4140 LPRINT "   INACCURATE FORECASTING"
4150 LPRINT
4160 LPRINT TAB(10); "*** END OF REPORT ***"
