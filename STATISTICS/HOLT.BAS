10 CLEAR
20 REM CHRISTOPHER D. VAN DER KAAY, PH.D. 2025
30 REM HOLT'S EXPONENTIAL SMOOTHING PROGRAM 1.2
40 REM FOR FORECASTING FUTURE DATA FROM HISTORICALS
50 REM PROVIDES MEAN SQUARE ERROR, MEAN ABSOLUTE PERCENTAGE ERROR, 
60 REM MEAN ABSOLUTE ERROR, AND ROOT MEAN SQUARE ERROR
70 PRINT
80 PRINT  "********************************************************"
90 PRINT  "*     WELCOME TO HOLT'S LINEAR TREND EXPONENTIAL       *"
100 PRINT "*                SMOOTHING PROGRAM 1.2                 *"
110 PRINT "*                                                      *"
120 PRINT "*      THIS PROGRAM USES HOLT'S METHOD TO FORECAST     *"
130 PRINT "*      FUTURE DATA POINTS BASED ON HISTORICAL DATA.    *"
140 PRINT "*                                                      *"
150 PRINT "*      IT CALCULATES AND PROVIDES THE FOLLOWING:       *"
160 PRINT "*      - MEAN SQUARE ERROR (MSE)                       *"
170 PRINT "*      - MEAN ABSOLUTE PERCENTAGE ERROR (MAPE)         *"
180 PRINT "*      - MEAN ABSOLUTE ERROR (MAE)                     *"
190 PRINT "*      - ROOT MEAN SQUARE ERROR (RMSE)                 *"
200 PRINT "*                                                      *"
210 PRINT "*      NOTE: THE PROGRAM MAY TAKE SEVERAL MINUTES      *"
220 PRINT "*      TO RUN, DEPENDING ON THE SIZE OF THE HISTORICAL *"
230 PRINT "*      DATA SET AND FORECAST HORIZON.                  *"
240 PRINT "********************************************************"
250 PRINT
260 PRINT "PRESS RETURN TO CONTINUE..."
270 INPUT DUMMY$
280 DIM Y(200), F(200), S(200), B(200)
290 INPUT "ENTER NUMBER OF HISTORICAL DATA POINTS (2-200): "; N
300 IF N < 2 OR N > 200 THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 290
310 FOR I = 1 TO N
320 INPUT "ENTER DATA POINT: "; A$
330 IF A$ = "" THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 320
340 GOSUB 2660
350 IF BADNUM THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 320
360 Y(I) = NUM
370 INPUT "IS THIS CORRECT (Y/N): "; OK$
380 IF OK$ = "Y" THEN GOTO 440
390 IF OK$ = "y" THEN GOTO 440
400 IF OK$ = "N" THEN GOTO 320
410 IF OK$ = "n" THEN GOTO 320
420 PRINT "PLEASE ENTER Y OR N."
430 GOTO 370
440 NEXT I
450 GOSUB 2190
460 INPUT "ENTER FORECAST HORIZON: "; H
470 H = INT(H)
480 IF H <= 0 THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 460
490 IF N + H > 200 THEN PRINT "INVALID INPUT. N+H MUST BE <= 200. TRY AGAIN.": GOTO 460
500 PRINT
510 PRINT "********************************************************"
520 PRINT "*                ALPHA AND BETA SELECTION              *"
530 PRINT "********************************************************"
540 PRINT "THE PROGRAM WILL NOW TEST DIFFERENT ALPHA AND BETA      "
550 PRINT "VALUES AND PROVIDE YOU WITH THE BEST COMBINATION OF     "
560 PRINT "ALPHA AND BETA BASED ON THE MEAN SQUARE ERROR. IT USES  "
570 PRINT "A NESTED LOOP TO TEST COMBINATIONS OF ALPHA AND BETA,   "
580 PRINT "WHICH CAN BE COMPUTATIONALLY EXPENSIVE. DEPENDING ON    "
590 PRINT "THE STEP SIZE AND RANGE FOR ALPHA AND BETA, THIS CAN    "
600 PRINT "LEAD TO MANY ITERATIONS AND MAY TAKE TIME TO RUN."
610 PRINT 
620 PRINT "PRESS RETURN TO CONTINUE..."
630 INPUT DUMMY$
640 BESTMSE = 1.701412E+38
650 PRINT " ALPHA |  BETA  |        MSE"
660 PRINT "----------------------------"
670 FOR ALPHA = .1 TO .99 STEP .02
680 PRINT CHR$(7): PRINT "TESTING ALPHA ="; ALPHA; "..."
690 FOR BETA = .1 TO .99 STEP .02
700 PRINT "  TESTING BETA ="; BETA; "..."
710 S(1) = Y(1)
720 B(1) = Y(2) - Y(1)
730 F(1) = Y(1)
740 FOR I = 2 TO N
750 F(I) = S(I-1) + B(I-1)
760 S(I) = ALPHA * Y(I) + (1 - ALPHA) * (S(I-1) + B(I-1))
770 B(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * B(I-1)
780 NEXT I
790 SSE = 0
800 FOR I = 2 TO N
810 SSE = SSE + (Y(I) - F(I)) ^ 2
820 NEXT I
830 IF N > 1 THEN MSE = SSE / (N - 1)
840 IF N <= 1 THEN MSE = 0
850 IF MSE >= BESTMSE THEN GOTO 890
860 BESTMSE = MSE
870 BESTALPHA = ALPHA
880 BESTBETA = BETA
890 PRINT " "; ALPHA; "  | "; BETA; "  | "; MSE
900 NEXT BETA
910 PRINT "----------------------------"
920 PRINT "CURRENT BEST: ALPHA = "; BESTALPHA; ", BETA = "; BESTBETA; ", MSE = "; BESTMSE
930 PRINT
940 NEXT ALPHA
950 PRINT
960 PRINT "********************************************************"
970 PRINT "*             BEST ALPHA AND BETA SELECTON             *"
980 PRINT "********************************************************"
990 PRINT "BEST ALPHA: "; BESTALPHA
1000 PRINT "BEST BETA: "; BESTBETA
1010 PRINT
1020 PRINT "PRESS RETURN TO VIEW ERROR/PERFORMANCE METRICS..."
1030 INPUT DUMMY$
1040 S(1) = Y(1)
1050 B(1) = Y(2) - Y(1)
1060 F(1) = Y(1)
1070 FOR I = 2 TO N
1080 F(I) = S(I-1) + B(I-1)
1090 S(I) = BESTALPHA * Y(I) + (1 - BESTALPHA) * (S(I-1) + B(I-1))
1100 B(I) = BESTBETA * (S(I) - S(I-1)) + (1 - BESTBETA) * B(I-1)
1110 NEXT I
1120 SSE = 0
1130 SAE = 0
1140 SAPE = 0
1150 COUNT = 0
1160 COUNTMAPE = 0
1170 EPS = .000001
1180 FOR I = 2 TO N
1190 E = Y(I) - F(I)
1200 SSE = SSE + E * E
1210 SAE = SAE + ABS(E)
1220 COUNT = COUNT + 1
1230 IF ABS(Y(I)) < EPS THEN GOTO 1260
1240 SAPE = SAPE + (ABS(E) / ABS(Y(I)))
1250 COUNTMAPE = COUNTMAPE + 1
1260 NEXT I
1270 IF COUNT > 0 THEN MSE = SSE / COUNT
1280 IF COUNT <= 0 THEN MSE = 0
1290 IF COUNT > 0 THEN MAE = SAE / COUNT
1300 IF COUNT <= 0 THEN MAE = 0
1310 IF COUNTMAPE > 0 THEN MAPE = (SAPE / COUNTMAPE) * 100
1320 IF COUNTMAPE <= 0 THEN MAPE = 0
1330 RMSE = SQR(MSE)
1340 FOR I = N+1 TO N+H
1350 F(I) = S(N) + B(N) * (I - N)
1360 NEXT I
1370 PRINT "********************************************************"
1380 PRINT "*                  CALCULATED RESULTS                  *"
1390 PRINT "********************************************************"
1400 PRINT "MEAN ABSOLUTE ERROR: "; MAE
1410 PRINT "  MAE IS THE AVERAGE OF ABSOLUTE DIFFERENCES"
1420 PRINT "  BETWEEN ACTUAL AND FORECASTED VALUES."
1430 PRINT "  INDICATES AVERAGE ERROR."
1440 PRINT "--------------------------------------------------------"
1450 PRINT "MEAN SQUARE ERROR: "; MSE
1460 PRINT "  MSE IS THE AVERAGE OF SQUARED DIFFERENCES."
1470 PRINT "  PENALIZES LARGER ERRORS MORE."
1480 PRINT "--------------------------------------------------------"
1490 PRINT "ROOT MEAN SQUARE ERROR: "; RMSE
1500 PRINT "  RMSE IS THE SQUARE ROOT OF MSE."
1510 PRINT "  MEASURES SPREAD OF ERRORS IN THE SAME UNITS"
1520 PRINT "  AS THE DATA."
1530 PRINT "--------------------------------------------------------"
1540 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
1550 PRINT "  MAPE IS THE AVERAGE OF ABSOLUTE PERCENTAGE ERRORS."
1560 PRINT "  NORMALIZES ERRORS AS PERCENTAGES."
1570 PRINT "--------------------------------------------------------"
1580 PRINT
1590 PRINT "PRESS RETURN TO VIEW FORECASTED VALUES..."
1600 INPUT DUMMY$
1610 PRINT "********************************************************"
1620 PRINT "*                   FORECASTED VALUES                  *"
1630 PRINT "********************************************************"
1640 LPP = 18: LC = 0
1650 FOR I = N+1 TO N+H
1660 HH = I - N
1670 PRINT "PERIOD "; HH; ": FORECAST = "; F(I)
1680 LC = LC + 1
1690 IF LC < LPP THEN GOTO 1730
1700 IF I = N+H THEN GOTO 1730
1710 INPUT "PRESS RETURN FOR MORE..."; DUMMY$
1720 LC = 0
1730 NEXT I
1740 PRINT
1750 INPUT "ENTER 1 TO VIEW RESULTS AGAIN, 2 TO RESTART, 3 TO PRINT REPORT, 4 TO QUIT: "; SELECT
1760 IF SELECT = 1 THEN GOTO 960
1770 IF SELECT = 2 THEN GOTO 10
1780 IF SELECT = 3 THEN GOSUB 1840:GOTO 1750
1790 IF SELECT = 4 THEN GOTO 1820
1800 PRINT "INVALID CHOICE. PLEASE ENTER 1, 2, 3, OR 4."
1810 GOTO 1750
1820 PRINT "GOODBYE!"
1830 END
1840 REM *** REPORT PRINT SUBROUTINE ***
1850 LPRINT "********************************************************"
1860 LPRINT "*                   CALCULATED RESULTS                 *"
1870 LPRINT "********************************************************"
1880 LPRINT "MEAN ABSOLUTE ERROR: "; MAE
1890 LPRINT "  MAE IS THE AVERAGE OF ABSOLUTE DIFFERENCES"
1900 LPRINT "  BETWEEN ACTUAL AND FORECASTED VALUES."
1910 LPRINT "  INDICATES AVERAGE ERROR."
1920 LPRINT "--------------------------------------------------------"
1930 LPRINT "MEAN SQUARE ERROR: "; MSE
1940 LPRINT "  MSE IS THE AVERAGE OF SQUARED DIFFERENCES."
1950 LPRINT "  PENALIZES LARGER ERRORS MORE."
1960 LPRINT "  USEFUL FOR IDENTIFYING MODELS THAT MINIMIZE"
1970 LPRINT "  LARGE ERRORS."
1980 LPRINT "--------------------------------------------------------"
1990 LPRINT "ROOT MEAN SQUARE ERROR: "; RMSE
2000 LPRINT "  RMSE IS THE SQUARE ROOT OF MSE."
2010 LPRINT "  MEASURES SPREAD OF ERRORS IN THE SAME UNITS"
2020 LPRINT "  AS THE DATA."
2030 LPRINT "--------------------------------------------------------"
2040 LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
2050 LPRINT "  MAPE IS THE AVERAGE OF ABSOLUTE PERCENTAGE ERRORS."
2060 LPRINT "  NORMALIZES ERRORS AS PERCENTAGES."
2070 LPRINT "  FACILITATES COMPARISONS ACROSS DIFFERENT SCALES."
2080 LPRINT "--------------------------------------------------------"
2090 LPRINT
2100 LPRINT "********************************************************"
2110 LPRINT "*                  FORECASTED VALUES                   *"
2120 LPRINT "********************************************************"
2130 FOR I = N+1 TO N+H
2140 HH = I - N
2150 LPRINT "PERIOD "; HH; ": FORECAST = "; F(I)
2160 NEXT I
2170 LPRINT "*** END OF REPORT ***"
2180 RETURN
2190 REM *** REVIEW / EDIT DATA SUBROUTINE (PAGED) ***
2200 LPP = 18
2210 START = 1
2220 PRINT
2230 PRINT "********************************************************"
2240 PRINT "*            REVIEW HISTORICAL DATA POINTS             *"
2250 PRINT "********************************************************"
2260 IEND = START + LPP - 1
2270 IF IEND > N THEN IEND = N
2280 FOR I = START TO IEND
2290 PRINT I; ": "; Y(I)
2300 NEXT I
2310 PRINT
2320 PRINT "N=NEXT  P=PREV  E=EDIT  Q=QUIT REVIEW"
2330 INPUT "CHOICE: "; CMD$
2340 IF CMD$ = "n" THEN CMD$ = "N"
2350 IF CMD$ = "p" THEN CMD$ = "P"
2360 IF CMD$ = "e" THEN CMD$ = "E"
2370 IF CMD$ = "q" THEN CMD$ = "Q"
2380 IF CMD$ = "N" THEN GOTO 2430
2390 IF CMD$ = "P" THEN GOTO 2470
2400 IF CMD$ = "E" THEN GOTO 2510
2410 IF CMD$ = "Q" THEN RETURN
2420 PRINT "PLEASE ENTER N, P, E, OR Q.": GOTO 2330
2430 REM NEXT PAGE
2440 IF IEND >= N THEN PRINT "END OF LIST.": GOTO 2330
2450 START = IEND + 1
2460 GOTO 2220
2470 REM PREV PAGE
2480 START = START - LPP
2490 IF START < 1 THEN START = 1
2500 GOTO 2220
2510 REM EDIT
2520 PRINT "ENTER DATA POINT NUMBER TO EDIT (1-"; N; ", 0=NONE): ";
2530 INPUT J
2540 J = INT(J)
2550 IF J = 0 THEN GOTO 2220
2560 IF J < 1 THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 2520
2570 IF J > N THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 2520
2580 PRINT "CURRENT VALUE "; J; " = "; Y(J)
2590 INPUT "ENTER NEW DATA POINT: "; A$
2600 IF A$ = "" THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 2590
2610 GOSUB 2660
2620 IF BADNUM THEN PRINT "INVALID INPUT. TRY AGAIN.": GOTO 2590
2630 Y(J) = NUM
2640 PRINT "UPDATED."
2650 GOTO 2220
2660 REM *** VALIDATE NUMERIC STRING IN A$; RETURN NUM AND BADNUM ***
2670 BADNUM = 0
2680 NUM = 0
2690 L = LEN(A$)
2700 IF L = 0 THEN BADNUM = 1: RETURN
2710 DIG = 0
2720 DOT = 0
2730 K = 1
2740 C$ = MID$(A$, K, 1)
2750 IF C$ = " " THEN K = K + 1: IF K <= L THEN GOTO 2740
2760 IF K > L THEN BADNUM = 1: RETURN
2770 C$ = MID$(A$, K, 1)
2780 IF C$ = "+" THEN K = K + 1
2790 IF C$ = "-" THEN K = K + 1
2800 IF K > L THEN BADNUM = 1: RETURN
2810 C$ = MID$(A$, K, 1)
2820 AC = ASC(C$)
2830 IF AC < 48 THEN GOTO 2890
2840 IF AC > 57 THEN GOTO 2890
2850 DIG = 1
2860 K = K + 1
2870 IF K <= L THEN GOTO 2810
2880 GOTO 2970
2890 IF C$ <> "." THEN GOTO 2970
2900 DOT = DOT + 1
2910 IF DOT > 1 THEN BADNUM = 1: RETURN
2920 K = K + 1
2930 IF K > L THEN GOTO 2950
2940 GOTO 2810
2950 IF DIG = 0 THEN BADNUM = 1: RETURN
2960 GOTO 2970
2970 IF DIG = 0 THEN BADNUM = 1: RETURN
2980 IF K > L THEN GOTO 3020
2990 C$ = MID$(A$, K, 1)
3000 IF C$ = " " THEN K = K + 1: GOTO 2980
3010 BADNUM = 1: RETURN
3020 NUM = VAL(A$)
3030 RETURN
