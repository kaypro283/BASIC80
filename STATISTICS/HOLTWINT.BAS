10 REM HOLT-WINTERS FORECASTING PROGRAM V1.2 BY C. VAN DER KAAY (2025)
20 PRINT  "***************************************************************"
30 PRINT  "*           HOLT-WINTERS FORECASTING PROGRAM V1.2             *"
40 PRINT  "***************************************************************"
50 PRINT  "*  THIS PROGRAM BUILDS A HOLT-WINTERS MODEL FOR TIME SERIES.  *"
60 PRINT  "*  IT ESTIMATES: LEVEL (S), TREND (T), AND SEASONALITY (C).   *"
70 PRINT  "*  SMOOTHING: ALPHA (LEVEL), BETA (TREND), GAMMA (SEASONAL).  *"
80 PRINT  "*  THE PROGRAM SEARCHES FOR GOOD ALPHA/BETA/GAMMA VALUES.     *"
90 PRINT "*  ADDITIVE (A), MULTIPLICATIVE (M), OR AUTO MODEL SELECT.    *"
100 PRINT "*  AUTO MODE TRIES BOTH AND PICKS THE LOWER RMSE RESULT.      *"
110 PRINT "*  ERROR METRICS: MPE, MAPE, MAE, RMSE + DIAGNOSTIC TESTS.    *"
120 PRINT "*  DURBIN-WATSON AND LJUNG-BOX Q ARE REPORTED FOR RESIDUALS.  *"
130 PRINT "*  SEASONAL PERIOD (P) IS DETECTED AUTOMATICALLY USING ACF.   *"
140 PRINT "*  TIP: LONG OUTPUT IS PAGED; PRESS RETURN WHEN PROMPTED.     *"
150 PRINT "***************************************************************"
160 PAUSED = 0
170 DEBUGMODE = 0
180 AUTORUN = 0
190 AUTOSTAGE = 0
200 MODELOCK = 0
210 LPP = 18   'LINES PER PAGE
220 PRINT 
230 PRINT "PRESS RETURN TO START OR TYPE 'INFO' FOR DETAILS"
240 PRINT "OR TYPE 'DEBUG' TO ENABLE DEBUG MODE FOR AUTOMATIC TESTING"
250 INPUT BEGIN$
260 IF BEGIN$ = "INFO" THEN GOSUB 6300 : PRINT : PRINT "RETURNING FROM INFO MENU..." : GOTO 230
270 IF BEGIN$ = "DEBUG" THEN DEBUGMODE = 1 : GOTO 300
280 IF BEGIN$ = "" THEN GOTO 300
290 PRINT "INVALID INPUT. PLEASE PRESS RETURN, OR TYPE 'INFO' OR 'DEBUG'." : GOTO 230
300 IF DEBUGMODE = 1 THEN GOTO 370
310 PRINT "SEASONALITY MODEL: ADDITIVE (A), MULTIPLICATIVE (M), OR AUTO (AUTO)"
320 INPUT SEASONALITY$
330 IF SEASONALITY$ = "A" THEN MODELTYPE = 1 : AUTORUN = 0 : GOTO 370
340 IF SEASONALITY$ = "M" THEN MODELTYPE = 2 : AUTORUN = 0 : GOTO 370
350 IF SEASONALITY$ = "AUTO" THEN AUTORUN = 1 : AUTOSTAGE = 1 : MODELTYPE = 1 : GOTO 370
360 PRINT "INVALID INPUT. ENTER 'A', 'M', OR 'AUTO'." : GOTO 310
370 DIM Y(600),F(600),S(600),T(600),C(600),U(600),L(600),E(600),ACFY(200),ACFRES(200),TEMPACF(100),CANDP(3),CANDVAL(3)
380 IF DEBUGMODE = 1 THEN GOSUB 7320 : GOTO 810
390 INPUT "ENTER NUMBER OF HISTORICAL DATA POINTS: "; N$
400 N = VAL(N$)
410 IF N <= 0 OR N <> INT(N) THEN PRINT "INVALID INPUT. PLEASE ENTER A POSITIVE INTEGER." : GOTO 390
420 FOR I = 1 TO N
430 INPUT "ENTER DATA POINT "; Y$
440 IF Y$ = "" THEN PRINT "INPUT CANNOT BE EMPTY." : I = I - 1 : GOTO 430
450 FOR J = 1 TO LEN(Y$)
460 C$ = MID$(Y$, J, 1)
470 IF (C$ < "0" OR C$ > "9") AND C$ <> "." AND C$ <> "-" THEN PRINT "INVALID INPUT. PLEASE ENTER A VALID NUMBER." : I = I - 1 : GOTO 430
480 NEXT J
490 Y(I) = VAL(Y$)
500 PRINT "YOU ENTERED: "; Y(I)
510 INPUT "IS THIS CORRECT? (Y/N): "; CORRECT$
520 IF CORRECT$ <> "Y" AND CORRECT$ <> "N" AND CORRECT$ <> "y" AND CORRECT$ <> "n" THEN PRINT "PLEASE TYPE Y OR N." : GOTO 510
530 IF CORRECT$ = "N" OR CORRECT$ = "n" THEN PRINT "PLEASE RE-ENTER THE CORRECT VALUE." : I = I - 1
540 NEXT I
550 IF MODELTYPE <> 2 THEN GOTO 650
560 FOR I = 1 TO N
570 IF Y(I) <= 0 THEN GOTO 620
580 NEXT I
590 GOTO 650
600 PRINT "PLEASE CHOOSE ADDITIVE (A) OR RE-ENTER DATA."
610 GOTO 310
620 PRINT "*** ERROR: MULTIPLICATIVE MODEL REQUIRES ALL DATA > 0 ***"
630 IF AUTORUN = 1 THEN PRINT "*** AUTO NOTE: SKIPPING MULTIPLICATIVE; USING ADDITIVE RESULTS ***" : MODELTYPE = 1 : BESTALPHA = ABAL : BESTBETA = ABBE : BESTGAMMA = ABGA : BESTMSE = ABMS : AUTORUN = 0 : AUTOSTAGE = 3 : GOTO 2660
640 GOTO 600
650 IF DEBUGMODE = 1 THEN GOTO 690
660 INPUT "ENTER FORECAST HORIZON (1-100): "; H$
670 H = VAL(H$)
680 IF H <= 0 OR H > 100 OR H <> INT(H) THEN PRINT "INVALID INPUT. PLEASE ENTER A NUMBER BETWEEN 1 AND 100." : GOTO 650
690 TRAINSIZE = N - H
700 IF TRAINSIZE >= 2 THEN GOTO 810
710 PRINT
720 PRINT "ERROR: NOT ENOUGH DATA FOR TRAINING."
730 PRINT "YOU ENTERED N="; N; " AND H="; H; " SO TRAINSIZE="; TRAINSIZE
740 PRINT "CHOOSE: (H) CHANGE FORECAST HORIZON, (D) RE-ENTER DATA, (M) MAIN MENU"
750 INPUT CH$
760 IF CH$ = "H" OR CH$ = "h" THEN GOTO 650
770 IF CH$ = "D" OR CH$ = "d" THEN GOTO 390
780 IF CH$ = "M" OR CH$ = "m" THEN GOTO 230
790 PRINT "INVALID CHOICE. TYPE H, D, OR M."
800 GOTO 750
810 REM *** CALCULATE SEASONAL PERIOD AUTOMATICALLY ***
820 PRINT
830 PRINT "CALCULATING SEASONAL PERIOD (ACF)... PLEASE WAIT."
840 PRINT "DATA POINTS (N)="; N; "  TRAIN SIZE="; TRAINSIZE; "  FORECAST H="; H
850 PRINT
860 GOSUB 5390 ' CALCULATE ACF AND DETERMINE P
870 IF P <= 0 OR P <> INT(P) THEN PRINT "WARNING: SEASONAL PERIOD NOT DETECTED. USING DEFAULT P = 12" : P = 12
880 IF 2 * P > 200 THEN PRINT "ERROR: SEASONAL PERIOD TOO LARGE.": END
890 PRINT:PRINT "************************************************************"
900 PRINT "DETECTED SEASONAL PERIOD: "; P
910 IF TRAINSIZE < 2 * P THEN PRINT "WARNING: TOO FEW FULL SEASONS FOR RELIABLE SEASONALITY" : REM CONTINUE ANYWAY
920 PRINT "************************************************************"
930 PRINT:PRINT "YOU HAVE SUCCESSFULLY ENTERED THE REQUIRED DATA."
940 PRINT "THE PROGRAM WILL NOW BEGIN TESTING SMOOTHING FACTORS."
950 IF AUTORUN = 1 THEN IF AUTOSTAGE = 2 THEN IF MODELOCK = 1 THEN GOTO 1020
960 PRINT "DO YOU WANT FAST MODE (80 COMBINATIONS) OR FULL MODE (810 COMBINATIONS)?"
970 PRINT "TYPE 'F' FOR FAST OR 'FULL' FOR COMPLETE SEARCH:"
980 INPUT MODE$
990 IF MODE$ = "F" THEN FASTMODE = 1 : MODELOCK = 1 : GOTO 1020
1000 IF MODE$ = "FULL" THEN FASTMODE = 0 : MODELOCK = 1 : GOTO 1020
1010 PRINT "INVALID INPUT. TYPE 'F' FOR FAST OR 'FULL' FOR COMPLETE SEARCH." : GOTO 980
1020 REM *** OPTIMIZE SMOOTHING FACTORS ***
1030 IF MODELTYPE = 1 THEN PRINT "*** OPTIMIZATION MODEL: ADDITIVE ***"
1040 IF MODELTYPE = 2 THEN PRINT "*** OPTIMIZATION MODEL: MULTIPLICATIVE ***"
1050 BESTMSE = 1E+30
1060 REM *** PROGRESS TRACKING VARIABLES ***
1070 COMBOCOUNT = 0
1080 IF FASTMODE = 1 THEN TOTALCOMBOS = 80 ELSE TOTALCOMBOS = 810
1090 IF FASTMODE = 1 THEN AISTART = 2 : AISTEP = 2 : AIEND = 8 ELSE AISTART = 1 : AISTEP = 1 : AIEND = 9
1100 FOR AI = AISTART TO AIEND STEP AISTEP
1110 ALPHA = AI / 10
1120 IF FASTMODE = 1 THEN BISTART = 2 : BISTEP = 2 : BIEND = 8 ELSE BISTART = 1 : BISTEP = 1 : BIEND = 9
1130 FOR BI = BISTART TO BIEND STEP BISTEP
1140 BETA = BI / 10
1150 IF FASTMODE = 1 THEN GISTART = 2 : GISTEP = 2 : GIEND = 10 ELSE GISTART = 1 : GISTEP = 1 : GIEND = 10
1160 FOR GI = GISTART TO GIEND STEP GISTEP
1170 GAMMA = GI / 10
1180 COMBOCOUNT = COMBOCOUNT + 1
1190 PERCENTDONE = INT((COMBOCOUNT / TOTALCOMBOS) * 100)
1200 FOR RST = 1 TO N
1210 S(RST) = 0 : T(RST) = 0 : C(RST) = 0
1220 NEXT RST
1230 FOR RST = 1 TO N + H
1240 F(RST) = 0 : E(RST) = 0
1250 NEXT RST
1260 MSE = 0
1270 PRINT:PRINT "TESTING COMBINATION "; COMBOCOUNT; " OF "; TOTALCOMBOS; ""
1280 PRINT "ALPHA = "; ALPHA; " BETA = "; BETA; " GAMMA = "; GAMMA 
1290 PRINT PERCENTDONE; "% COMPLETE"
1300 S(1) = Y(1)
1310 REM *** TREND INITIALIZATION ***
1320 SUMT = 0
1330 COUNT = 0
1340 FOR J = 1 TO TRAINSIZE - 1
1350 SUMT = SUMT + (Y(J + 1) - Y(J))
1360 COUNT = COUNT + 1
1370 NEXT J
1380 IF COUNT > 0 THEN T(1) = SUMT / COUNT ELSE T(1) = 0
1390 REM *** TREND T(1) INITIALIZED AS AVERAGE SLOPE OVER TRAINING DATA ***
1400 YMEAN = 0
1410 FOR I = 1 TO TRAINSIZE
1420 YMEAN = YMEAN + Y(I)
1430 NEXT I
1440 YMEAN = YMEAN / TRAINSIZE
1450 IF MODELTYPE = 1 THEN GOTO 1480
1460 IF MODELTYPE = 2 THEN GOTO 1960
1470 GOTO 1780
1480 FOR I = 1 TO P
1490 SUMC = 0
1500 COUNT = 0
1510 FOR J = 0 TO INT((TRAINSIZE - I) / P)
1520 IDX = I + J * P
1530 IF IDX <= TRAINSIZE THEN SUMC = SUMC + (Y(IDX) - YMEAN) : COUNT = COUNT + 1
1540 NEXT J
1550 IF COUNT > 0 THEN C(I) = SUMC / COUNT ELSE C(I) = 0
1560 NEXT I
1570 REM *** NORMALIZE ADDITIVE SEASONALS: SUM(C(1..P)) SHOULD BE ~ 0 ***
1580 CAVG = 0
1590 FOR J = 1 TO P
1600 CAVG = CAVG + C(J)
1610 NEXT J
1620 CAVG = CAVG / P
1630 FOR J = 1 TO P
1640 C(J) = C(J) - CAVG
1650 NEXT J
1660 FOR I = P + 1 TO TRAINSIZE
1670 C(I) = C(I - P)
1680 NEXT I
1690 REM *** WARM-UP STATES: COMPUTE S(2..P), T(2..P) SO S(P),T(P) NOT ZERO ***
1700 FOR I = 2 TO P
1710 REM *** WARM-UP: UPDATE S,T ONLY; KEEP INITIAL SEASONALS FIXED (CONSISTENT WITH LATER C(I-P)) ***
1720 IF MODELTYPE = 1 THEN S(I) = ALPHA * (Y(I) - C(I)) + (1 - ALPHA) * (S(I-1) + T(I-1)) : T(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * T(I-1)
1730 IF MODELTYPE = 2 THEN EPS = .000001
1740 IF MODELTYPE = 2 THEN IF ABS(C(I)) < EPS THEN C(I) = EPS
1750 IF MODELTYPE = 2 THEN S(I) = ALPHA * (Y(I) / C(I)) + (1 - ALPHA) * (S(I-1) + T(I-1))
1760 IF MODELTYPE = 2 THEN T(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * T(I-1)
1770 REM *** NOTE: DO NOT UPDATE C() UNTIL I = P+1 (MAIN LOOP USES C(I-P)) ***
1780 NEXT I
1790 FOR I = P + 1 TO TRAINSIZE
1800 IF MODELTYPE = 1 THEN GOTO 1830
1810 IF MODELTYPE = 2 THEN GOTO 1880
1820 GOTO 2160
1830 REM *** ADDITIVE UPDATE ***
1840 S(I) = ALPHA * (Y(I) - C(I-P)) + (1 - ALPHA) * (S(I-1) + T(I-1))
1850 T(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * T(I-1)
1860 C(I) = GAMMA * (Y(I) - S(I)) + (1 - GAMMA) * C(I-P)
1870 GOTO 2160
1880 REM *** MULTIPLICATIVE UPDATE (WARN ONLY WHEN NEEDED) ***
1890 EPS = .000001
1900 IF ABS(C(I-P)) < EPS THEN PRINT "*** WARNING: C(I-P) ~ 0 IN MULTIPLICATIVE UPDATE AT STEP "; I : C(I-P) = EPS
1910 S(I) = ALPHA * (Y(I) / C(I-P)) + (1 - ALPHA) * (S(I-1) + T(I-1))
1920 T(I) = BETA * (S(I) - S(I-1)) + (1 - BETA) * T(I-1)
1930 IF ABS(S(I)) < EPS THEN PRINT "*** WARNING: S(I) ~ 0 IN MULTIPLICATIVE UPDATE AT STEP "; I : S(I) = EPS
1940 C(I) = GAMMA * (Y(I) / S(I)) + (1 - GAMMA) * C(I-P)
1950 GOTO 2160
1960 FOR J = 1 TO P
1970 SUMC = 0
1980 COUNT = 0
1990 FOR K = 0 TO INT((TRAINSIZE - J) / P)
2000 IDX = J + K * P
2010 IF IDX <= TRAINSIZE AND YMEAN <> 0 THEN SUMC = SUMC + (Y(IDX) / YMEAN) : COUNT = COUNT + 1
2020 NEXT K
2030 IF COUNT > 0 THEN C(J) = SUMC / COUNT ELSE C(J) = 1
2040 NEXT J
2050 REM *** NORMALIZE MULTIPLICATIVE SEASONALS: MEAN(C(1..P)) SHOULD BE ~ 1 ***
2060 CAVG = 0
2070 FOR J2 = 1 TO P
2080 CAVG = CAVG + C(J2)
2090 NEXT J2
2100 CAVG = CAVG / P
2110 IF CAVG = 0 THEN CAVG = 1
2120 FOR J2 = 1 TO P
2130 C(J2) = C(J2) / CAVG
2140 NEXT J2
2150 GOTO 1660
2160 NEXT I
2170 REM *** END TRAINING UPDATE LOOP FOR THIS COMBINATION ***
2180 FOR K = P + 1 TO TRAINSIZE
2190 IF ABS(S(K)) > 1E+10 OR ABS(C(K)) > 1E+10 THEN PRINT "UNSTABLE FOR ALPHA="; ALPHA; " BETA="; BETA; " GAMMA="; GAMMA : GOTO 2440
2200 NEXT K
2210 REM *** CALCULATE FORECAST AND MSE ***
2220 FOR I = 1 TO H
2230 TEMP = ((TRAINSIZE + I - 1) MOD P) + 1
2240 SEASPOS = TRAINSIZE - P + TEMP
2250 IF MODELTYPE = 1 THEN F(TRAINSIZE+I) = S(TRAINSIZE) + I * T(TRAINSIZE) + C(SEASPOS)
2260 IF MODELTYPE = 2 THEN F(TRAINSIZE+I) = (S(TRAINSIZE) + I * T(TRAINSIZE)) * C(SEASPOS)
2270 NEXT I
2280 FOR I = 1 TO H
2290 E(I) = 0
2300 E(I) = Y(TRAINSIZE + I) - F(TRAINSIZE + I)
2310 MSE = MSE + E(I) * E(I)
2320 NEXT I
2330 MSE = MSE / H
2340 IF MSE = 0 THEN PRINT "*** WARNING: MSE=0 — CHECK FOR POSSIBLE MODEL DEGENERACY! ***"
2350 PRINT "MSE FOR ALPHA="; ALPHA; " BETA="; BETA; " GAMMA="; GAMMA; ": "; MSE
2360 IF MSE < BESTMSE THEN GOTO 2380
2370 GOTO 2440
2380 BESTMSE = MSE
2390 BESTALPHA = ALPHA
2400 BESTBETA = BETA
2410 BESTGAMMA = GAMMA
2420 REM *** END OF BEST VALUE ASSIGNMENT ***
2430 REM *** CONTINUES TO NEXT GAMMA ***
2440 NEXT GI
2450 NEXT BI
2460 PRINT "************************************************************"
2470 NEXT AI
2480 PRINT "OPTIMAL ALPHA: "; BESTALPHA
2490 PRINT "OPTIMAL BETA: "; BESTBETA
2500 PRINT "OPTIMAL GAMMA: "; BESTGAMMA
2510 PRINT "BEST MEAN SQUARED ERROR: "; BESTMSE
2520 IF AUTORUN <> 1 THEN GOTO 2580
2530 IF AUTOSTAGE <> 1 THEN GOTO 2560
2540 ABAL = BESTALPHA: ABBE = BESTBETA: ABGA = BESTGAMMA: ABMS = BESTMSE
2550 GOTO 2580
2560 IF AUTOSTAGE <> 2 THEN GOTO 2580
2570 MBAL = BESTALPHA: MBBE = BESTBETA: MBGA = BESTGAMMA: MBMS = BESTMSE
2580 PRINT "************************************************************"
2590 PRINT "OPTIMAL SMOOTHING FACTORS FOUND."
2600 PRINT "THE PROGRAM WILL NOW USE THESE FACTORS TO COMPUTE FORECASTS"
2610 PRINT "AND CALCULATE ERROR METRICS."
2620 IF AUTORUN = 1 AND AUTOSTAGE < 3 THEN GOTO 2660
2630 PRINT : PRINT "PRESS RETURN TO CONTINUE."
2640 INPUT CONTINUE$
2650 GOTO 2660
2660 REM *** USE OPTIMAL SMOOTHING FACTORS ***
2670 ALPHA = BESTALPHA
2680 BETA = BESTBETA
2690 GAMMA = BESTGAMMA
2700 S(1) = Y(1)
2710 REM *** TREND INIT MATCHING SEARCH ***
2720 SUMT = 0 : COUNT = 0
2730 FOR J = 1 TO TRAINSIZE - 1
2740 SUMT = SUMT + (Y(J + 1) - Y(J)) : COUNT = COUNT + 1
2750 NEXT J
2760 IF COUNT > 0 THEN T(1) = SUMT / COUNT ELSE T(1) = 0
2770 REM *** MEAN OF TRAINING DATA ***
2780 YMEAN = 0
2790 FOR J = 1 TO TRAINSIZE
2800 YMEAN = YMEAN + Y(J)
2810 NEXT J
2820 YMEAN = YMEAN / TRAINSIZE
2830 REM *** SEASONAL INIT MATCHING SEARCH ***
2840 IF MODELTYPE = 1 THEN GOTO 2860
2850 IF MODELTYPE = 2 THEN GOTO 3050
2860 REM *** ADDITIVE SEASONALS *** 
2870 FOR J = 1 TO P
2880 SUMC = 0 : COUNT = 0
2890 FOR K = 0 TO INT((TRAINSIZE - J) / P)
2900 IDX = J + K * P
2910 IF IDX <= TRAINSIZE THEN SUMC = SUMC + (Y(IDX) - YMEAN) : COUNT = COUNT + 1
2920 NEXT K
2930 IF COUNT > 0 THEN C(J) = SUMC / COUNT ELSE C(J) = 0
2940 NEXT J
2950 REM *** NORMALIZE ADDITIVE SEASONALS: SUM(C(1..P)) SHOULD BE ~ 0 ***
2960 CAVG = 0
2970 FOR JN = 1 TO P
2980 CAVG = CAVG + C(JN)
2990 NEXT JN
3000 CAVG = CAVG / P
3010 FOR JN = 1 TO P
3020 C(JN) = C(JN) - CAVG
3030 NEXT JN
3040 GOTO 3240
3050 REM *** MULTIPLICATIVE SEASONALS ***
3060 FOR J = 1 TO P
3070 SUMC = 0 : COUNT = 0
3080 FOR K = 0 TO INT((TRAINSIZE - J) / P)
3090 IDX = J + K * P
3100 IF IDX <= TRAINSIZE AND YMEAN <> 0 THEN SUMC = SUMC + (Y(IDX) / YMEAN) : COUNT = COUNT + 1
3110 NEXT K
3120 IF COUNT > 0 THEN C(J) = SUMC / COUNT ELSE C(J) = 1
3130 NEXT J
3140 REM *** NORMALIZE MULTIPLICATIVE SEASONALS: MEAN(C(1..P)) SHOULD BE ~ 1 ***
3150 CAVG = 0
3160 FOR JM = 1 TO P
3170 CAVG = CAVG + C(JM)
3180 NEXT JM
3190 CAVG = CAVG / P
3200 IF CAVG = 0 THEN CAVG = 1
3210 FOR JM = 1 TO P
3220 C(JM) = C(JM) / CAVG
3230 NEXT JM
3240 FOR J = P + 1 TO TRAINSIZE
3250 C(J) = C(J - P)
3260 NEXT J
3270 REM *** WARM-UP STATES: COMPUTE S(2..P), T(2..P) SO S(P),T(P) NOT ZERO ***
3280 FOR J = 2 TO P
3290 REM *** WARM-UP: UPDATE S,T ONLY; KEEP INITIAL SEASONALS FIXED (CONSISTENT WITH LATER C(J-P)) ***
3300 IF MODELTYPE = 1 THEN S(J) = ALPHA * (Y(J) - C(J)) + (1 - ALPHA) * (S(J-1) + T(J-1)) : T(J) = BETA * (S(J) - S(J-1)) + (1 - BETA) * T(J-1)
3310 IF MODELTYPE = 2 THEN EPS = .000001
3320 IF MODELTYPE = 2 THEN IF ABS(C(J)) < EPS THEN C(J) = EPS
3330 IF MODELTYPE = 2 THEN S(J) = ALPHA * (Y(J) / C(J)) + (1 - ALPHA) * (S(J-1) + T(J-1))
3340 IF MODELTYPE = 2 THEN T(J) = BETA * (S(J) - S(J-1)) + (1 - BETA) * T(J-1)
3350 REM *** NOTE: DO NOT UPDATE C() UNTIL J = P+1 (MAIN LOOP USES C(J-P)) ***
3360 NEXT J
3370 FOR J = P + 1 TO TRAINSIZE
3380 IF MODELTYPE = 1 THEN S(J) = ALPHA * (Y(J) - C(J-P)) + (1 - ALPHA) * (S(J-1) + T(J-1)) : T(J) = BETA * (S(J) - S(J-1)) + (1 - BETA) * T(J-1) : C(J) = GAMMA * (Y(J) - S(J)) + (1 - GAMMA) * C(J-P)
3390 IF MODELTYPE <> 2 THEN GOTO 3460
3400 EPS = .000001
3410 IF ABS(C(J-P)) < EPS THEN PRINT "*** WARNING: C(J-P) ~ 0 IN MULTIPLICATIVE UPDATE AT STEP "; J : C(J-P) = EPS
3420 S(J) = ALPHA * (Y(J) / C(J-P)) + (1 - ALPHA) * (S(J-1) + T(J-1))
3430 T(J) = BETA * (S(J) - S(J-1)) + (1 - BETA) * T(J-1)
3440 IF ABS(S(J)) < EPS THEN PRINT "*** WARNING: S(J) ~ 0 IN MULTIPLICATIVE UPDATE AT STEP "; J : S(J) = EPS
3450 C(J) = GAMMA * (Y(J) / S(J)) + (1 - GAMMA) * C(J-P)
3460 NEXT J
3470 FOR I = P + 1 TO TRAINSIZE
3480 IF MODELTYPE = 1 THEN F(I) = S(I - 1) + T(I - 1) + C(I - P)
3490 IF MODELTYPE = 2 THEN F(I) = (S(I - 1) + T(I - 1)) * C(I - P)
3500 E(I) = Y(I) - F(I)
3510 NEXT I
3520 REM *** FORECAST DATA ***
3530 FOR I = 1 TO H
3540 TEMP = ((TRAINSIZE + I - 1) MOD P) + 1
3550 SEASPOS = TRAINSIZE - P + TEMP
3560 IF MODELTYPE = 1 THEN F(TRAINSIZE + I) = S(TRAINSIZE) + I * T(TRAINSIZE) + C(SEASPOS)
3570 IF MODELTYPE = 2 THEN F(TRAINSIZE + I) = (S(TRAINSIZE) + I * T(TRAINSIZE)) * C(SEASPOS)
3580 NEXT I
3590 REM *** SE FOR INTERVALS: USE IN-SAMPLE ONE-STEP RESIDUALS (MORE STABLE) ***
3600 SSE = 0
3610 ECNT = 0
3620 FOR I = P + 1 TO TRAINSIZE
3630 SSE = SSE + E(I) * E(I)
3640 ECNT = ECNT + 1
3650 NEXT I
3660 IF ECNT = 0 THEN SE = 0 ELSE SE = SQR(SSE / ECNT)
3670 PRINT "STANDARD ERROR USED FOR INTERVALS: "; SE
3680 REM *** KEEP HELD-OUT MSE FOR REPORTING / SCORING ***
3690 MSE = 0
3700 FOR I = 1 TO H
3710 MSE = MSE + (Y(TRAINSIZE + I) - F(TRAINSIZE + I)) ^ 2
3720 NEXT I
3730 MSE = MSE / H
3740 REM *** CALCULATE MEAN OF TRAINING DATA ***
3750 YAVG = 0
3760 FOR I = 1 TO TRAINSIZE
3770 YAVG = YAVG + Y(I)
3780 NEXT I
3790 YAVG = YAVG / TRAINSIZE
3800 REM *** CALCULATE STANDARD DEVIATION OF TRAINING DATA ***
3810 SUMSQR = 0
3820 FOR I = 1 TO TRAINSIZE
3830 SUMSQR = SUMSQR + (Y(I) - YAVG) ^ 2
3840 NEXT I
3850 YSTD = SQR(SUMSQR / (TRAINSIZE - 1))
3860 REM *** RELATIVE STANDARD ERROR CHECK ***
3870 IF YAVG <> 0 THEN IF SE / YAVG > .25 THEN PRINT "*** WARNING: FORECAST ERROR > 25% OF DATA MEAN — POSSIBLY UNSTABLE MODEL ***"
3880 REM *** SE COMPARED TO IN-SAMPLE VOLATILITY ***
3890 IF SE > YSTD THEN PRINT "*** WARNING: FORECAST ERROR > DATA VOLATILITY — CHECK MODEL FIT OR P ***"
3900 REM *** PRINT FORECASTS WITH PREDICTION INTERVALS ***
3910 IF AUTORUN = 1 AND AUTOSTAGE < 3 THEN GOTO 4060
3920 PRINT CHR$(7): PRINT "*********************************************"
3930 PRINT "FORECASTS WITH PREDICTION INTERVALS:"
3940 FOR I = 1 TO H
3950 TEMP = ((TRAINSIZE + I - 1) MOD P) + 1
3960 SEASPOS = TRAINSIZE - P + TEMP
3970 IF MODELTYPE = 1 THEN F(TRAINSIZE + I) = S(TRAINSIZE) + I * T(TRAINSIZE) + C(SEASPOS)
3980 IF MODELTYPE = 2 THEN F(TRAINSIZE + I) = (S(TRAINSIZE) + I * T(TRAINSIZE)) * C(SEASPOS)
3990 U(TRAINSIZE + I) = F(TRAINSIZE + I) + 1.96 * SE * SQR(I)
4000 L(TRAINSIZE + I) = F(TRAINSIZE + I) - 1.96 * SE * SQR(I)
4010 IF L(TRAINSIZE + I) < 0 THEN L(TRAINSIZE + I) = 0
4020 PRINT "STEP"; I; " | FORECAST:"; F(TRAINSIZE + I); " | UPPER:"; U(TRAINSIZE + I); " | LOWER:"; L(TRAINSIZE + I)
4030 IF I MOD LPP = 0 THEN GOSUB 5350
4040 NEXT I
4050 PRINT "*********************************************"
4060 REM *** PRINT FORECASTS AND ERRORS ***
4070 MPE = 0
4080 MAPE = 0
4090 MAE = 0
4100 RMSE = 0
4110 PAUSED = 0
4120 VALIDH = 0
4130 SUME2 = 0
4140 FOR I = 1 TO H
4150 IF Y(TRAINSIZE + I) = 0 THEN PRINT "DIVISION BY ZERO AT STEP "; I : GOTO 4230
4160 VALIDH = VALIDH + 1
4170 E(I) = (Y(TRAINSIZE + I) - F(TRAINSIZE + I)) / Y(TRAINSIZE + I)
4180 SUME2 = SUME2 + E(I) * E(I)
4190 MPE = MPE + E(I)
4200 MAPE = MAPE + ABS(E(I))
4210 MAE = MAE + ABS(Y(TRAINSIZE + I) - F(TRAINSIZE + I))
4220 RMSE = RMSE + (Y(TRAINSIZE + I) - F(TRAINSIZE + I)) ^ 2
4230 NEXT I
4240 IF VALIDH = 0 THEN PRINT "*** ERROR: ALL HELD-OUT VALUES WERE ZERO; CANNOT COMPUTE PERCENT ERRORS ***" : GOTO 4460
4250 MPE = MPE / VALIDH * 100
4260 MAPE = MAPE / VALIDH * 100
4270 PRINT "NOTE: ANY FORECAST STEPS WITH DIVISION BY ZERO WERE SKIPPED."
4280 MAE = MAE / VALIDH
4290 RMSE = SQR(RMSE / VALIDH)
4300 IF AUTORUN = 1 AND AUTOSTAGE < 3 THEN GOTO 4340
4310 PRINT "MEAN PERCENTAGE ERROR: "; MPE; "%"
4320 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
4330 PRINT "MEAN ABSOLUTE ERROR: "; MAE
4340 PRINT "ROOT MEAN SQUARED ERROR: "; RMSE
4350 IF AUTORUN <> 1 THEN GOTO 4460
4360 IF AUTORUN = 1 AND AUTOSTAGE = 1 THEN PRINT : PRINT "*** AUTO MODE: STAGE 1 COMPLETE (ADDITIVE). RMSE="; RMSE
4370 IF AUTORUN = 1 AND AUTOSTAGE = 1 THEN PRINT "*** AUTO MODE: NOW STARTING STAGE 2 (MULTIPLICATIVE) ***"
4380 IF AUTOSTAGE = 1 THEN ADDRMSE = RMSE: AUTOSTAGE = 2: MODELTYPE = 2: GOTO 550
4390 MULTRMSE = RMSE
4400 IF MULTRMSE < ADDRMSE THEN MODELTYPE = 2: BESTALPHA = MBAL: BESTBETA = MBBE: BESTGAMMA = MBGA: BESTMSE = MBMS: GOTO 4420
4410 MODELTYPE = 1: BESTALPHA = ABAL: BESTBETA = ABBE: BESTGAMMA = ABGA: BESTMSE = ABMS
4420 AUTORUN = 0: AUTOSTAGE = 3
4430 IF MODELTYPE = 1 THEN PRINT "*** AUTO MODE PICKED: ADDITIVE (MODELTYPE=1) — LOWER RMSE ***"
4440 IF MODELTYPE = 2 THEN PRINT "*** AUTO MODE PICKED: MULTIPLICATIVE (MODELTYPE=2) — LOWER RMSE ***"
4450 GOTO 2660
4460 REM *** DURBIN-WATSON STATISTIC ***
4470 DWNUMERATOR = 0
4480 DWDENOMINATOR = 0
4490 PAUSED = 0
4500 FOR I = P + 2 TO TRAINSIZE
4510 DWNUMERATOR = DWNUMERATOR + (E(I) - E(I-1)) ^ 2
4520 DWDENOMINATOR = DWDENOMINATOR + E(I) ^ 2
4530 NEXT I
4540 IF DWDENOMINATOR = 0 THEN PRINT "*** DURBIN-WATSON UNDEFINED: ZERO RESIDUAL VARIANCE ***" ELSE DURBINWATSON = DWNUMERATOR / DWDENOMINATOR : PRINT "DURBIN-WATSON STATISTIC: "; DURBINWATSON
4550 REM *** LJUNG-BOX Q TEST ***
4560 Q = 0
4570 EMEAN = 0
4580 FOR I = P + 1 TO TRAINSIZE
4590 EMEAN = EMEAN + E(I)
4600 NEXT I
4610 EMEAN = EMEAN / (TRAINSIZE - P)
4620 DEN = 0
4630 FOR T = P + 1 TO TRAINSIZE
4640 DEN = DEN + (E(T) - EMEAN) * (E(T) - EMEAN)
4650 NEXT T
4660 IF DEN = 0 THEN PRINT "*** LJUNG-BOX SKIPPED: RESIDUAL VARIANCE = 0 ***" : GOTO 4780
4670 FOR K = 1 TO 10
4680 SUMRK = 0
4690 FOR T = P + K + 1 TO TRAINSIZE
4700 SUMRK = SUMRK + (E(T) - EMEAN) * (E(T - K) - EMEAN)
4710 NEXT T
4720 RK = SUMRK / DEN
4730 Q = Q + (RK * RK) / (TRAINSIZE - P - K)
4740 IF K MOD 10 = 0 THEN GOSUB 5350
4750 NEXT K
4760 Q = Q * (TRAINSIZE - P) * ((TRAINSIZE - P) + 2)
4770 PRINT "LJUNG-BOX Q STATISTIC: "; Q
4780 PRINT "*********************************************"
4790 REM *** CALCULATE AUTOCORRELATION FUNCTION (ACF) FOR THE HISTORICAL DATA SERIES Y ***
4800 REM *** AUTOCORRELATION FUNCTION (ACF) CALCULATION ***
4810 YMEAN = 0
4820 FOR I = 1 TO N
4830 YMEAN = YMEAN + Y(I)
4840 NEXT I
4850 YMEAN = YMEAN / N
4860 FOR LAG = 1 TO 2 * P
4870 NUMERATOR = 0
4880 DENOMINATOR = 0
4890 FOR I = 1 TO N - LAG
4900 NUMERATOR = NUMERATOR + (Y(I) - YMEAN) * (Y(I + LAG) - YMEAN)
4910 NEXT I
4920 FOR I = 1 TO N
4930 DENOMINATOR = DENOMINATOR + (Y(I) - YMEAN) * (Y(I) - YMEAN)
4940 NEXT I
4950 IF DENOMINATOR = 0 THEN ACFY(LAG) = 0 ELSE ACFY(LAG) = NUMERATOR / DENOMINATOR
4960 PRINT "ACF FOR HISTORICAL DATA AT LAG "; LAG; ": "; ACFY(LAG)
4970 IF LAG MOD LPP = 0 THEN GOSUB 5350
4980 NEXT LAG
4990 PRINT "*********************************************"
5000 REM *** CALCULATE AUTOCORRELATION FUNCTION (ACF) FOR RESIDUALS ***
5010 PRINT "CALCULATING ACF FOR RESIDUALS..."
5020 EMEAN = 0
5030 FOR I = P + 1 TO TRAINSIZE
5040 EMEAN = EMEAN + E(I)
5050 NEXT I
5060 EMEAN = EMEAN / (TRAINSIZE - P)
5070 FOR LAG = 1 TO 2 * P
5080 NUMERATOR = 0
5090 DENOMINATOR = 0
5100 FOR I = P + 1 TO TRAINSIZE - LAG
5110 NUMERATOR = NUMERATOR + (E(I) - EMEAN) * (E(I + LAG) - EMEAN)
5120 NEXT I
5130 FOR J = P + 1 TO TRAINSIZE
5140 DENOMINATOR = DENOMINATOR + (E(J) - EMEAN) * (E(J) - EMEAN)
5150 NEXT J
5160 IF DENOMINATOR = 0 THEN ACFRES(LAG) = 0 ELSE ACFRES(LAG) = NUMERATOR / DENOMINATOR
5170 PRINT "ACF FOR RESIDUALS AT LAG "; LAG; ": "; ACFRES(LAG)
5180 IF LAG MOD LPP = 0 THEN GOSUB 5350
5190 NEXT LAG
5200 REM *** END OF RESIDUAL ACF LOOP ***
5210 REM *** PROMPT USER TO PRINT REPORT OR EXIT OR RESTART OR VIEW ***
5220 PRINT "WOULD YOU LIKE TO VIEW RESULTS AGAIN, PRINT, EXIT, OR RESTART?"
5230 PRINT "ENTER 'V' TO VIEW, 'P' TO PRINT, 'E' TO EXIT, 'R' TO RESTART"
5240 INPUT CHOICE$
5250 IF CHOICE$ = "v" THEN CHOICE$ = "V"
5260 IF CHOICE$ = "p" THEN CHOICE$ = "P"
5270 IF CHOICE$ = "e" THEN CHOICE$ = "E"
5280 IF CHOICE$ = "r" THEN CHOICE$ = "R"
5290 IF CHOICE$ = "V" THEN GOSUB 7500 : GOTO 5220
5300 IF CHOICE$ = "P" THEN GOSUB 5740 : GOTO 5220
5310 IF CHOICE$ = "E" THEN GOTO 6210
5320 IF CHOICE$ = "R" THEN GOTO 10
5330 PRINT "INVALID CHOICE. PLEASE ENTER 'V', 'P', 'E', OR 'R'."
5340 GOTO 5220
5350 REM *** SUBROUTINE FOR PAGINATION ***
5360 PRINT : PRINT "PRESS RETURN TO CONTINUE..."
5370 INPUT PAUSE$
5380 RETURN
5390 REM *** SUBROUTINE TO CALCULATE ACF AND DETERMINE P (FIXED) ***
5400 P = 0
5410 YMEAN = 0
5420 FOR I = 1 TO N
5430 YMEAN = YMEAN + Y(I)
5440 NEXT I
5450 YMEAN = YMEAN / N
5460 SUMDEN = 0
5470 FOR I = 1 TO N
5480 SUMDEN = SUMDEN + (Y(I) - YMEAN) ^ 2
5490 NEXT I
5500 IF SUMDEN = 0 THEN P = 0 : RETURN
5510 MAXLAG = INT(N / 2)
5520 IF MAXLAG > 100 THEN MAXLAG = 100
5530 IF MAXLAG < 2 THEN P = 0 : RETURN
5540 FOR LAG = 1 TO MAXLAG
5550 SUMNUM = 0
5560 FOR I = 1 TO N - LAG
5570 SUMNUM = SUMNUM + (Y(I) - YMEAN) * (Y(I + LAG) - YMEAN)
5580 NEXT I
5590 TEMPACF(LAG) = SUMNUM / SUMDEN
5600 NEXT LAG
5610 THRESHOLD = 1.96 / SQR(N)
5620 MINP = 4
5630 IF MINP > MAXLAG - 1 THEN MINP = 2
5640 BESTVAL = 0
5650 FOR LAG = MINP TO MAXLAG - 1
5660 IF TEMPACF(LAG) > THRESHOLD THEN IF TEMPACF(LAG) > TEMPACF(LAG - 1) AND TEMPACF(LAG) >= TEMPACF(LAG + 1) THEN IF TEMPACF(LAG) > BESTVAL THEN BESTVAL = TEMPACF(LAG) : P = LAG
5670 NEXT LAG
5680 IF P <> 0 THEN RETURN
5690 BESTVAL = -1E+30
5700 FOR LAG = 2 TO MAXLAG
5710 IF TEMPACF(LAG) > BESTVAL THEN BESTVAL = TEMPACF(LAG) : P = LAG
5720 NEXT LAG
5730 RETURN
5740 REM *** PRINT REPORT SUBROUTINE ***
5750 REM *** CALCULATE YAVG AND YSTD FOR WARNINGS ***
5760 YAVG = 0
5770 FOR I = 1 TO TRAINSIZE
5780 YAVG = YAVG + Y(I)
5790 NEXT I
5800 YAVG = YAVG / TRAINSIZE
5810 SUMSQR = 0
5820 FOR I = 1 TO TRAINSIZE
5830 SUMSQR = SUMSQR + (Y(I) - YAVG) ^ 2
5840 NEXT I
5850 YSTD = SQR(SUMSQR / (TRAINSIZE - 1))
5860 LPRINT "********** HOLT-WINTERS FORECASTING REPORT **********"
5870 IF MODELTYPE = 1 THEN LPRINT "MODEL TYPE: ADDITIVE"
5880 IF MODELTYPE = 2 THEN LPRINT "MODEL TYPE: MULTIPLICATIVE"
5890 LPRINT "SEASONAL PERIOD: "; P
5900 LPRINT "OPTIMAL ALPHA: "; BESTALPHA
5910 LPRINT "OPTIMAL BETA: "; BESTBETA
5920 LPRINT "OPTIMAL GAMMA: "; BESTGAMMA
5930 LPRINT "BEST MEAN SQUARED ERROR: "; BESTMSE
5940 LPRINT "MEAN PERCENTAGE ERROR: "; MPE; "%"
5950 LPRINT "NOTE: ERROR METRICS BASED ON HELD-OUT FORECASTS."
5960 LPRINT "      RESIDUALS USED FOR DIAGNOSTIC TESTING ONLY."
5970 LPRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
5980 LPRINT "MEAN ABSOLUTE ERROR: "; MAE
5990 LPRINT "ROOT MEAN SQUARED ERROR: "; RMSE
6000 LPRINT "STANDARD ERROR USED FOR PREDICTION INTERVALS: "; SE
6010 IF SE / YAVG > .25 THEN LPRINT "*** WARNING: FORECAST ERROR > 25% OF DATA MEAN — MODEL MAY BE UNSTABLE ***"
6020 IF SE > YSTD THEN LPRINT "*** WARNING: FORECAST ERROR > DATA VOLATILITY — CHECK MODEL FIT OR SEASONALITY ***"
6030 LPRINT "DURBIN-WATSON STATISTIC: "; DURBINWATSON
6040 LPRINT "LJUNG-BOX Q STATISTIC: "; Q
6050 LPRINT "**************************----"
6060 LPRINT "FORECASTS WITH PREDICTION INTERVALS:"
6070 FOR I = 1 TO H
6080 LPRINT "STEP "; I; " | FORECAST: "; F(TRAINSIZE + I); " | UPPER: "; U(TRAINSIZE + I); " | LOWER: "; L(TRAINSIZE + I)
6090 NEXT I
6100 LPRINT "**************************----"
6110 LPRINT "AUTOCORRELATION FUNCTION (ACF) VALUES FOR HISTORICAL DATA:"
6120 FOR LAG = 1 TO 2 * P
6130 LPRINT "ACF FOR LAG "; LAG; ": "; ACFY(LAG)
6140 NEXT LAG
6150 LPRINT "AUTOCORRELATION FUNCTION (ACF) VALUES FOR RESIDUALS:"
6160 FOR LAG = 1 TO 2 * P
6170 LPRINT "ACF FOR LAG "; LAG; ": "; ACFRES(LAG)
6180 NEXT LAG
6190 LPRINT "********** END OF REPORT **********"
6200 RETURN
6210 REM *** END CREDITS ***
6220 PRINT "******************************************************"
6230 PRINT "*          HOLT-WINTERS FORECASTING PROGRAM          *"
6240 PRINT "*                        V1.2                        *"
6250 PRINT "******************************************************"
6260 PRINT "*    WRITTEN BY CHRISTOPHER D. VAN DER KAAY, PH.D.   *"
6270 PRINT "*                        2025                        *"
6280 PRINT "******************************************************"
6290 END
6300 REM *** INFORMATION MENU SUBROUTINE ***
6310 PRINT "INFORMATION MENU"
6320 PRINT "************************************"
6330 PRINT "1. ABOUT THIS PROGRAM"
6340 PRINT "2. WHEN TO USE HOLT-WINTERS"
6350 PRINT "3. STATISTICS DETAILS"
6360 PRINT "4. RETURN TO MAIN MENU"
6370 PRINT "************************************"
6380 PRINT
6390 INPUT "SELECT AN OPTION (1-4): "; INFO$
6400 IF INFO$ = "1" THEN GOSUB 6470 : GOTO 6300
6410 IF INFO$ = "2" THEN GOSUB 6750 : GOTO 6300
6420 IF INFO$ = "3" THEN GOSUB 6910 : GOTO 6300
6430 IF INFO$ = "4" THEN GOTO 6460
6440 PRINT "INVALID OPTION. PLEASE ENTER 1-4."
6450 GOTO 6300
6460 RETURN
6470 PRINT "ABOUT THIS PROGRAM"
6480 PRINT "*********************************************"
6490 PRINT "THIS PROGRAM IMPLEMENTS THE HOLT-WINTERS"
6500 PRINT "METHOD FOR TIME SERIES FORECASTING WITH"
6510 PRINT "LEVEL, TREND, AND SEASONAL COMPONENTS."
6520 PRINT "SMOOTHING FACTORS ALPHA, BETA, AND GAMMA"
6530 PRINT "ARE OPTIMIZED AUTOMATICALLY BY MINIMIZING"
6540 PRINT "FORECAST ERROR OVER A HELD-OUT SAMPLE."
6550 PRINT "BOTH ADDITIVE AND MULTIPLICATIVE MODELS"
6560 PRINT "ARE SUPPORTED, WITH AN OPTIONAL AUTO MODE"
6570 PRINT "THAT SELECTS THE BEST MODEL BASED ON RMSE."
6580 PRINT "THE PROGRAM ALSO COMPUTES ERROR METRICS"
6590 PRINT "AND DIAGNOSTIC TESTS FOR MODEL VALIDATION."
6600 PRINT
6610 PRINT "PRESS RETURN TO RETURN."
6620 INPUT PAUSE$
6630 PRINT "ADDITIVE AND MULTIPLICATIVE SEASONALITY"
6640 PRINT "*********************************************"
6650 PRINT "ADDITIVE MODE ASSUMES CONSTANT SEASONAL"
6660 PRINT "EFFECTS, WHILE MULTIPLICATIVE MODE SCALES"
6670 PRINT "SEASONALITY WITH THE LEVEL OF THE SERIES."
6680 PRINT "AUTO MODE EVALUATES BOTH AND SELECTS THE"
6690 PRINT "MODEL WITH THE LOWER OUT-OF-SAMPLE ERROR."
6700 PRINT "*********************************************"
6710 PRINT
6720 PRINT "PRESS RETURN TO RETURN."
6730 INPUT PAUSE$
6740 RETURN
6750 PRINT "WHEN TO USE HOLT-WINTERS"
6760 PRINT "*********************************************"
6770 PRINT "HOLT-WINTERS SHOULD BE USED WHEN YOUR DATA"
6780 PRINT "EXHIBITS BOTH TREND AND SEASONALITY. THIS"
6790 PRINT "METHOD IS IDEAL FOR TIME SERIES DATA WHERE"
6800 PRINT "SEASONAL PATTERNS AND TRENDS ARE EXPECTED"
6810 PRINT "TO CONTINUE. EXAMPLES INCLUDE:"
6820 PRINT " 1. MONTHLY SALES FIGURES"
6830 PRINT " 2. QUARTERLY REVENUE DATA"
6840 PRINT " 3. ANNUAL TEMPERATURE READINGS"
6850 PRINT "IT IS LESS SUITABLE FOR DATA WITHOUT CLEAR"
6860 PRINT "SEASONAL OR TREND COMPONENTS."
6870 PRINT
6880 PRINT "PRESS RETURN TO RETURN."
6890 INPUT PAUSE$
6900 RETURN
6910 PRINT "STATISTICS DETAILS"
6920 PRINT "*********************************************"
6930 PRINT "MPE: MEAN PERCENTAGE ERROR"
6940 PRINT " - MEASURES AVERAGE PERCENTAGE ERROR."
6950 PRINT " - INDICATES WHETHER FORECASTS ARE"
6960 PRINT "   CONSISTENTLY TOO HIGH OR LOW."
6970 PRINT
6980 PRINT "MAPE: MEAN ABSOLUTE PERCENTAGE ERROR"
6990 PRINT " - AVERAGE OF ABSOLUTE PERCENTAGE ERRORS."
7000 PRINT " - USEFUL FOR COMPARING FORECAST ACCURACY"
7010 PRINT "   ACROSS DIFFERENT DATASETS."
7020 PRINT
7030 PRINT "PRESS RETURN FOR MORE DETAILS."
7040 INPUT PAUSE$
7050 PRINT "*********************************************"
7060 PRINT "RMSE: ROOT MEAN SQUARED ERROR"
7070 PRINT " - MEASURES THE SQUARE ROOT OF THE AVERAGE"
7080 PRINT "   OF SQUARED ERRORS."
7090 PRINT " - INDICATES THE SPREAD OF FORECAST ERRORS."
7100 PRINT
7110 PRINT "MAE: MEAN ABSOLUTE ERROR"
7120 PRINT " - AVERAGE OF ABSOLUTE ERRORS."
7130 PRINT " - SHOWS THE AVERAGE MAGNITUDE OF ERRORS"
7140 PRINT "   WITHOUT CONSIDERING THEIR DIRECTION."
7150 PRINT
7160 PRINT "PRESS RETURN FOR MORE DETAILS."
7170 INPUT PAUSE$
7180 PRINT "*********************************************"
7190 PRINT "DURBIN-WATSON: AUTOCORRELATION TEST"
7200 PRINT " - TESTS FOR AUTOCORRELATION IN RESIDUALS"
7210 PRINT "   FROM A REGRESSION ANALYSIS."
7220 PRINT " - VALUES CLOSE TO 2 SUGGEST NO AUTOCORREL."
7230 PRINT
7240 PRINT "LJUNG-BOX Q: AUTOCORRELATION TEST"
7250 PRINT " - TESTS FOR PRESENCE OF AUTOCORRELATION"
7260 PRINT "   IN TIME SERIES DATA."
7270 PRINT " - SIGNIFICANT Q INDICATES NON-RANDOMNESS."
7280 PRINT
7290 PRINT "PRESS RETURN TO RETURN."
7300 INPUT PAUSE$
7310 RETURN
7320 REM *** DEBUG MODE SUBROUTINE ***
7330 AUTORUN = 1
7340 AUTOSTAGE = 1
7350 MODELTYPE = 1  ' START ADDITIVE; AUTO LOGIC FLIPS LATER
7360 PRINT : PRINT "*** DEBUG MODE ENABLED - POPULATING TEST DATA ***"
7370 MODELTYPE = 1  ' 1 FOR ADDITIVE, 2 FOR MULTIPLICATIVE
7380 N = 100
7390 H = 6
7400 TRAINSIZE = N - H
7410 FOR I = 1 TO N
7420 Y(I) = 100 + 10 * SIN(2 * 3.14 * I / 12) + .5 * I  ' SIMPLE SEASONAL TREND WITH NOISE
7430 NEXT I
7440 PRINT "AUTO MODE DEBUG: STAGE "; AUTOSTAGE; " (STARTING WITH ADDITIVE)"
7450 PRINT "DATA POINTS LOADED: "; N
7460 PRINT "FORECAST HORIZON: "; H
7470 PRINT "DEBUG DATA LOADED SUCCESSFULLY"
7480 PRINT "NOW JUMPING TO MAIN FORECAST ROUTINE. PLEASE BE PATIENT..."
7490 RETURN
7500 REM *** VIEW REPORT ONSCREEN (REPLAY) ***
7510 PRINT CHR$(7)
7520 PRINT "********** HOLT-WINTERS RESULTS (REVIEW) **********"
7530 IF MODELTYPE = 1 THEN PRINT "MODEL TYPE: ADDITIVE"
7540 IF MODELTYPE = 2 THEN PRINT "MODEL TYPE: MULTIPLICATIVE"
7550 PRINT "SEASONAL PERIOD: "; P
7560 PRINT "OPTIMAL ALPHA: "; BESTALPHA
7570 PRINT "OPTIMAL BETA: "; BESTBETA
7580 PRINT "OPTIMAL GAMMA: "; BESTGAMMA
7590 PRINT "BEST MEAN SQUARED ERROR: "; BESTMSE
7600 PRINT "MEAN PERCENTAGE ERROR: "; MPE; "%"
7610 PRINT "MEAN ABSOLUTE PERCENTAGE ERROR: "; MAPE; "%"
7620 PRINT "MEAN ABSOLUTE ERROR: "; MAE
7630 PRINT "ROOT MEAN SQUARED ERROR: "; RMSE
7640 PRINT "STANDARD ERROR USED FOR PREDICTION INTERVALS: "; SE
7650 PRINT "DURBIN-WATSON STATISTIC: "; DURBINWATSON
7660 PRINT "LJUNG-BOX Q STATISTIC: "; Q
7670 GOSUB 5350
7680 PRINT "FORECASTS WITH PREDICTION INTERVALS:"
7690 FOR I = 1 TO H
7700 PRINT "STEP"; I; " | FORECAST:"; F(TRAINSIZE + I); " | UPPER:"; U(TRAINSIZE + I); " | LOWER:"; L(TRAINSIZE + I)
7710 IF I MOD LPP = 0 THEN GOSUB 5350
7720 NEXT I
7730 GOSUB 5350
7740 PRINT "ACF VALUES FOR HISTORICAL DATA:"
7750 FOR LAG = 1 TO 2 * P
7760 PRINT "ACF FOR LAG "; LAG; ": "; ACFY(LAG)
7770 IF LAG MOD LPP = 0 THEN GOSUB 5350
7780 NEXT LAG
7790 GOSUB 5350
7800 PRINT "ACF VALUES FOR RESIDUALS:"
7810 FOR LAG = 1 TO 2 * P
7820 PRINT "ACF FOR LAG "; LAG; ": "; ACFRES(LAG)
7830 IF LAG MOD LPP = 0 THEN GOSUB 5350
7840 NEXT LAG
7850 PRINT "********** END OF REVIEW **********"
7860 RETURN
