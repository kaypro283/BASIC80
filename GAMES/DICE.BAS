10   CLEAR
20   REM *** DICE GAME ***
30   REM *** C. VAN DER KAAY 6/2024 ***
40  RANDOMIZE
50  REM -- DISPLAY TITLE AND INSTRUCTIONS --
60  GOSUB 410: REM TITLE SCREEN
70  GOSUB 560: REM PAUSE BEFORE INSTRUCTIONS
80  GOSUB 620: REM DISPLAY DETAILED INSTRUCTIONS
90  GOSUB 970: REM PAUSE BEFORE STARTING GAME ROUNDS
100  REM -- INITIALIZE SCORES AND CHOOSE NUMBER OF ROUNDS --
110  PLAYERSCORE = 0
120  COMPUTERSCORE = 0
130 NUMROUNDS = INT(RND * 5 + 1)
140 PRINT "THE COMPUTER HAS CHOSEN "; NUMROUNDS; " ROUNDS.": PRINT CHR$(7)
150 REM -- MAIN GAME LOOP --
160 FOR R = 1 TO NUMROUNDS
170 PRINT: PRINT "ROUND "; R
180 REM -- DETERMINE ROUND MODIFIER --
190 REM MODIFIER IS DETERMINED BY A RANDOM NUMBER FROM 0 TO 9:
200 REM   0-4: NORMAL ROUND, 5-8: DOUBLE POINTS, 9: TRIPLE POINTS (RARE)
210 MODRND = INT(RND * 10)
220 MODIFIER = 0
230 IF MODRND >= 5 THEN MODIFIER = 1
240 IF MODRND = 9 THEN MODIFIER = 2
250 IF MODIFIER = 1 THEN PRINT "SPECIAL ROUND: DOUBLE POINTS!"
260 IF MODIFIER = 2 THEN PRINT "SPECIAL ROUND: TRIPLE POINTS!"
270 GOSUB 1050: REM PLAYER'S TURN
280 GOSUB 1340: REM COMPUTER'S TURN
290 PRINT "CURRENT SCORES: YOU = "; PLAYERSCORE; "   COMPUTER = "; COMPUTERSCORE
300 GOSUB 1910: REM PAUSE BETWEEN ROUNDS
310 NEXT R
320 REM -- FINAL SCORES AND WINNER ANNOUNCEMENT --
330 GOSUB 1600: REM DISPLAY FINAL SCORES
340 GOSUB 1680: REM DETERMINE WINNER
350 REM -- PROMPT TO PLAY AGAIN --
360 GOSUB 1770: REM PLAY AGAIN SUBROUTINE
370 END
380 REM ============================
390 REM ***  TITLE SCREEN SUBROUTINE  ***
400 REM ============================
410 PRINT CHR$(26)
420 PRINT "============================"
430 PRINT "|     ______    ______     |"
440 PRINT "|    |      |  |      |    |"
450 PRINT "|    | O  O |  | O  O |    |"
460 PRINT "|    |  O   |  | O  O |    |"
470 PRINT "|    | O  O |  | O  O |    |"
480 PRINT "|    |______|  |______|    |"
490 PRINT "|   *WELCOME TO THE DICE*  |"
500 PRINT "|   *     G A M E !     *  |"
510 PRINT "============================": PRINT CHR$(7)
520 RETURN
530 REM ====================================
540 REM *** PAUSE BEFORE INSTRUCTIONS  ***
550 REM ====================================
560 PRINT "PRESS ENTER TO VIEW THE INSTRUCTIONS."
570 INPUT DUMMY$
580 RETURN
590 REM ====================================
600 REM ***  GAME INSTRUCTIONS  ***
610 REM ====================================
620 PRINT CHR$(26)
630 PRINT "==============================="
640 PRINT "        DICE GAME RULES        "
650 PRINT "==============================="
660 PRINT "1. ROUNDS:"
670 PRINT "   - THE COMPUTER RANDOMLY SELECTS"
680 PRINT "     BETWEEN 1 AND 5 ROUNDS PER GAME."
690 PRINT "2. DICE ROLLING:"
700 PRINT "   - IN EACH ROUND, YOU AND THE COMPUTER"
710 PRINT "     EACH ROLL TWO SIX-SIDED DICE."
720 PRINT "   - IF THE SUM OF YOUR DICE IS 7 OR MORE,"
730 PRINT "     YOU QUALIFY TO SCORE POINTS."
740 PRINT "3. BONUS ROLL ON DOUBLES:"
750 PRINT "   - IF BOTH DICE SHOW THE SAME NUMBER,"
760 PRINT "     YOU GET A BONUS ROLL (WITH A SHORT ANIMATION)."
770 PRINT "   - THE BONUS DIE'S VALUE IS ADDED TO YOUR TOTAL."
780 INPUT DUMMY$
790 PRINT "4. ROUND MODIFIERS:"
800 PRINT "   - AT THE START OF EACH ROUND, A RANDOM MODIFIER"
810 PRINT "     IS APPLIED:"
820 PRINT "       0: NORMAL ROUND."
830 PRINT "       1: DOUBLE POINTS (YOUR SCORE IS DOUBLED)."
840 PRINT "       2: TRIPLE POINTS (YOUR SCORE IS TRIPLED, RARE EVENT)."
850 PRINT "5. SCORING:"
860 PRINT "   - ONLY ROLLS WITH A TOTAL OF 7 OR MORE (AFTER BONUS)"
870 PRINT "     EARN POINTS, ADJUSTED BY THE ROUND MODIFIER."
880 PRINT "6. WINNING:"
890 PRINT "   - THE HIGHEST CUMULATIVE SCORE AFTER ALL ROUNDS WINS."
900 PRINT "===============================": PRINT CHR$(7)
910 PRINT "PRESS ENTER TO CONTINUE."
920 INPUT DUMMY$
930 RETURN
940 REM ====================================
950 REM *** PAUSE BEFORE GAME STARTS  ***
960 REM ====================================
970 PRINT CHR$(7)
980 PRINT "THE COMPUTER IS CHOOSING THE NUMBER OF ROUNDS."
990 PRINT "PRESS ENTER TO CONTINUE."
1000 INPUT DUMMY$
1010 RETURN
1020 REM ===========================================
1030 REM ***  PLAYER'S TURN: ROLLING THE DICE  ***
1040 REM ===========================================
1050 PRINT "PRESS ENTER TO ROLL YOUR DICE."
1060 INPUT DUMMY$
1070 REM -- DICE ROLL --
1080 PRINT "ROLLING DICE..."
1090 PLAYERROLL1 = INT(RND * 6 + 1)
1100 PLAYERROLL2 = INT(RND * 6 + 1)
1110 PRINT "YOU ROLLED: "; PLAYERROLL1; " AND "; PLAYERROLL2
1120 PLAYERTOTAL = PLAYERROLL1 + PLAYERROLL2
1130 PRINT "TOTAL = "; PLAYERTOTAL
1140 IF PLAYERROLL1 = PLAYERROLL2 THEN GOTO 1160
1150 GOTO 1230
1160 PRINT "DOUBLES! YOU GET A BONUS ROLL."
1170 PRINT "ROLLING BONUS DIE..."
1180 BONUS = INT(RND * 6 + 1)
1190 PRINT "BONUS ROLL: "; BONUS
1200 PLAYERTOTAL = PLAYERTOTAL + BONUS
1210 PRINT "NEW TOTAL WITH BONUS = "; PLAYERTOTAL
1220 GOTO 1230
1230 IF PLAYERTOTAL < 7 THEN GOTO 1290
1240 IF MODIFIER = 1 THEN PLAYERTOTAL = PLAYERTOTAL * 2: PRINT "DOUBLE POINTS ROUND! YOUR POINTS DOUBLED TO "; PLAYERTOTAL
1250 IF MODIFIER = 2 THEN PLAYERTOTAL = PLAYERTOTAL * 3: PRINT "TRIPLE POINTS ROUND! YOUR POINTS TRIPLED TO "; PLAYERTOTAL
1260 PLAYERSCORE = PLAYERSCORE + PLAYERTOTAL
1270 PRINT "YOU SCORE "; PLAYERTOTAL; " POINTS THIS ROUND."
1280 GOTO 1300
1290 PRINT "NO POINTS SCORED THIS ROUND."
1300 RETURN
1310 REM ============================================
1320 REM ***  COMPUTER'S TURN: ROLLING THE DICE  ***
1330 REM ============================================
1340 PRINT "COMPUTER IS ROLLING..."
1350 COMPUTERROLL1 = INT(RND * 6 + 1)
1360 COMPUTERROLL2 = INT(RND * 6 + 1)
1370 PRINT "THE COMPUTER ROLLED: "; COMPUTERROLL1; " AND "; COMPUTERROLL2
1380 COMPUTERTOTAL = COMPUTERROLL1 + COMPUTERROLL2
1390 PRINT "TOTAL = "; COMPUTERTOTAL
1400 IF COMPUTERROLL1 = COMPUTERROLL2 THEN GOTO 1420
1410 GOTO 1490
1420 PRINT "COMPUTER ROLLED DOUBLES AND GETS A BONUS ROLL."
1430 PRINT "COMPUTER ROLLING BONUS DIE..."
1440 BONUS = INT(RND * 6 + 1)
1450 PRINT "BONUS ROLL: "; BONUS
1460 COMPUTERTOTAL = COMPUTERTOTAL + BONUS
1470 PRINT "NEW TOTAL WITH BONUS = "; COMPUTERTOTAL
1480 GOTO 1490
1490 IF COMPUTERTOTAL < 7 THEN GOTO 1550
1500 IF MODIFIER = 1 THEN COMPUTERTOTAL = COMPUTERTOTAL * 2: PRINT "DOUBLE POINTS ROUND! COMPUTER'S POINTS DOUBLED TO "; COMPUTERTOTAL
1510 IF MODIFIER = 2 THEN COMPUTERTOTAL = COMPUTERTOTAL * 3: PRINT "TRIPLE POINTS ROUND! COMPUTER'S POINTS TRIPLED TO "; COMPUTERTOTAL
1520 COMPUTERSCORE = COMPUTERSCORE + COMPUTERTOTAL
1530 PRINT "COMPUTER SCORES "; COMPUTERTOTAL; " POINTS THIS ROUND."
1540 GOTO 1560
1550 PRINT "COMPUTER SCORES NO POINTS THIS ROUND."
1560 RETURN
1570 REM ==================================
1580 REM ***  DISPLAY FINAL SCORES  ***
1590 REM ==================================
1600 PRINT CHR$(12)
1610 PRINT "GAME OVER": PRINT CHR$(7)
1620 PRINT "YOUR FINAL SCORE = "; PLAYERSCORE
1630 PRINT "COMPUTER'S FINAL SCORE = "; COMPUTERSCORE
1640 RETURN
1650 REM =====================================
1660 REM ***  DETERMINE AND ANNOUNCE WINNER  ***
1670 REM =====================================
1680 IF PLAYERSCORE > COMPUTERSCORE THEN GOTO 1740
1690 IF COMPUTERSCORE > PLAYERSCORE THEN GOTO 1750
1700 PRINT "IT'S A TIE!": PRINT CHR$(7): GOTO 1760
1710 REM ====================================
1720 REM ***  PLAY AGAIN SUBROUTINE  ***
1730 REM ====================================
1740 PRINT "CONGRATULATIONS, YOU WIN!": PRINT CHR$(7): GOTO 1760
1750 PRINT "SORRY, THE COMPUTER WINS.": PRINT CHR$(7)
1760 RETURN
1770 PRINT
1780 INPUT "DO YOU WANT TO PLAY AGAIN? (Y/N) "; ANSWER$
1790 IF LEN(ANSWER$) = 0 THEN PRINT "PLEASE ENTER Y OR N.": GOTO 1780
1800 ANSWER$ = LEFT$(ANSWER$,1)
1810 C = ASC(ANSWER$)
1820 IF C < 97 THEN GOTO 1850
1830 IF C > 122 THEN GOTO 1850
1840 ANSWER$ = CHR$(C-32)
1850 IF ANSWER$ = "Y" THEN GOTO 10
1860 IF ANSWER$ = "N" THEN PRINT "GOODBYE!": PRINT CHR$(7): END
1870 PRINT "PLEASE ENTER Y OR N.": GOTO 1780
1880 REM ========================================
1890 REM ***  PAUSE BETWEEN ROUNDS SUBROUTINE  ***
1900 REM ========================================
1910 PRINT: PRINT "PRESS ENTER TO CONTINUE TO THE NEXT ROUND."
1920 INPUT DUMMY$
1930 RETURN
