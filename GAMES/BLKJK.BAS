10 REM *** BLACKJACK BY C.D. VAN DER KAAY V1.3 12/18/2025 ***
20 PRINT "TO VARY THE SHUFFLE EACH TIME, ENTER A RANDOM SEED."
30 PRINT "(OR JUST PICK ANY NUMBER IN THE RANGE SHOWN).": RANDOMIZE
40 PLAYERSURRD = 0  ' INITIALIZE PLAYER SURRENDERED FLAG
50 DIM CARD(312), VALUE(312), PLAYERCARD(52), DEALERCARD(52)  ' BIGGER HAND ARRAYS TO PREVENT SUBSCRIPT ERRORS
60 DIM PLAYERVALUE(52), DEALERVALUE(52)
70 DIM DISP$(13)
80 DISP$(1)="A":DISP$(2)="2":DISP$(3)="3":DISP$(4)="4":DISP$(5)="5"
90 DISP$(6)="6":DISP$(7)="7":DISP$(8)="8":DISP$(9)="9":DISP$(10)="10"
100 DISP$(11)="J":DISP$(12)="Q":DISP$(13)="K"
110 FIRSTMOVE = 1  ' FLAG TO TRACK IF IT IS THE PLAYER'S FIRST MOVE
120 DEALERPLAYD = 0  ' INITIALIZE DEALER PLAYED FLAG
130 PLAYERBUSTD = 0  ' INITIALIZE PLAYER BUSTED FLAG
140 INSURANCEPLACED = 0
150 INSURANCEBET = 0
160 TOTALINSURANCES = 0
170 INSURANCEWINS = 0
180 TOTALDOUBLES = 0
190 PLAYERSURRS = 0  ' INITIALIZE PLAYER SURRENDER COUNTER
200 GOSUB 2030  ' INITIALIZE STATISTICS
210 GOSUB 2310  ' DISPLAY TITLE SCREEN
220 GOSUB 2590  ' DISPLAY INSTRUCTIONS
230 GOSUB 4990  ' INITIAL SHUFFLE
240 TOTALRESHUFFLES = 0
250 GOSUB 4830 ' DEALER OPENING MESSAGE SUBROUTINE
260 BANKROLL = 1000  ' INITIALIZE PLAYER'S BANKROLL
270 LASTBET = 0   ' REMEMBER LAST BET FOR "R" REPEAT OPTION
280 REM START OF GAME LOOP
290 IF BANKROLL <= 0 THEN GOSUB 3820: GOTO 330  ' END GAME IF BANKROLL IS ZERO OR LESS
300 FIRSTMOVE = 1  ' ENSURE FIRSTMOVE IS SET TO 1 AT THE START OF EACH GAME
310 REM GOSUB 4740  ' SHUFFLE ONLY WHEN SHOE IS LOW/EMPTY
320 GOSUB 390  ' MAIN GAME SUBROUTINE
330 INPUT "DO YOU WANT TO PLAY AGAIN? (Y/N) "; A$
340 GOSUB 3520  ' CONVERT INPUT TO UPPERCASE
350 IF UPPERA$ = "Y" THEN GOTO 280  ' RESTART GAME
360 IF UPPERA$ = "N" THEN GOSUB 3650: GOSUB 3990: GOTO 5140
370 PRINT "INVALID INPUT. PLEASE ENTER Y OR N."
380 GOTO 330  ' ASK AGAIN
390 REM *** SUBROUTINE: MAIN GAME SUBROUTINE ***
400 REM INITIALIZE CARDS AND VALUES
410 REM INITIALIZE GAME STATE
420 DEALERPLAYD = 0  ' RESET DEALER PLAYED FLAG
430 PLAYERBUSTD = 0  ' RESET PLAYER BUSTED FLAG
440 PLAYERTOTAL = 0
450 DEALERTOTAL = 0
460 PLAYERCOUNT = 0
470 DEALERCOUNT = 0
480 REM NEXTCARD IS MAINTAINED ACROSS HANDS (SIX-DECK SHOE)
490 PRINT "**************************************"
500 PRINT "CURRENT BANKROLL: $"; BANKROLL
510 INPUT "ENTER YOUR BET (S=STATS, R=REPEAT LAST): $"; BET$
520 A$ = BET$: GOSUB 3520
530 IF UPPERA$ = "S" THEN GOSUB 4060: GOTO 500
540 IF UPPERA$ = "R" THEN GOTO 610
550 GOSUB 4270
560 IF VALIDBET = 0 THEN PRINT "INVALID BET! ENTER A NUMBER, S, OR R.": GOTO 510
570 BET = VAL(BET$)
580 IF BET < 1 OR BET > BANKROLL THEN PRINT "INVALID BET AMOUNT!": GOTO 510
590 LASTBET = BET
600 GOTO 660
610 IF LASTBET < 1 THEN PRINT "NO PREVIOUS BET TO REPEAT.": GOTO 510
620 BET = LASTBET
630 IF BET > BANKROLL THEN PRINT "LAST BET EXCEEDS CURRENT BANKROLL!": GOTO 510
640 PRINT "REPEATING LAST BET: $"; BET
650 GOTO 660
660 TOTALMONEYBET = TOTALMONEYBET + BET
670 IF NEXTCARD > 260 THEN PRINT "RESHUFFLING THE SHOE...": GOSUB 4990
680 REM INITIAL DEAL
690 FOR I = 1 TO 2
700 PLAYERCOUNT = PLAYERCOUNT + 1
710 PLAYERCARD(PLAYERCOUNT) = CARD(NEXTCARD)
720 PLAYERVALUE(PLAYERCOUNT) = VALUE(PLAYERCARD(PLAYERCOUNT))
730 PLAYERTOTAL = PLAYERTOTAL + PLAYERVALUE(PLAYERCOUNT)
740 NEXTCARD = NEXTCARD + 1
750 IF I = 1 THEN GOTO 760 ELSE GOTO 820
760 DEALERCOUNT = DEALERCOUNT + 1  ' DEAL ONE CARD TO DEALER (VISIBLE CARD)
770 DEALERCARD(DEALERCOUNT) = CARD(NEXTCARD)
780 DEALERVALUE(DEALERCOUNT) = VALUE(DEALERCARD(DEALERCOUNT))
790 DEALERTOTAL = DEALERTOTAL + DEALERVALUE(DEALERCOUNT)
800 NEXTCARD = NEXTCARD + 1
810 GOTO 820
820 NEXT I  ' REPEAT FOR THE SECOND CARD
830 DEALERCOUNT = DEALERCOUNT + 1  ' DEAL SECOND CARD TO DEALER (HIDDEN CARD)
840 DEALERCARD(DEALERCOUNT) = CARD(NEXTCARD)
850 DEALERVALUE(DEALERCOUNT) = VALUE(DEALERCARD(DEALERCOUNT))
860 C = DEALERCARD(1): GOSUB 5590: IF D$ = "A" THEN GOSUB 5340
870 TEMPTOTAL = DEALERTOTAL + DEALERVALUE(2)
880 IF TEMPTOTAL = 21 AND PLAYERTOTAL = 21 THEN GOTO 900
890 GOTO 950
900 IF INSURANCEPLACED = 1 THEN PRINT "INSURANCE PAYS 2:1. PROFIT $"; INSURANCEBET * 2
910 IF INSURANCEPLACED = 1 THEN BANKROLL = BANKROLL + INSURANCEBET * 3
920 IF INSURANCEPLACED = 1 THEN INSURANCEWINS = INSURANCEWINS + 1
930 INSURANCEPLACED = 0: INSURANCEBET = 0
940 PRINT "PUSH! BOTH YOU AND THE DEALER HAVE BLACKJACK.": GOSUB 2260: RETURN
950 IF TEMPTOTAL = 21 THEN GOSUB 5500: RETURN
960 IF INSURANCEPLACED = 1 THEN INSURANCEPLACED = 0: INSURANCEBET = 0
970 REM DO NOT ADD HIDDEN CARD TO DEALERTOTAL YET
980 NEXTCARD = NEXTCARD + 1
990 REM CHECK IF DEALER AND PLAYER BOTH HAVE 21 AFTER THE INITIAL DEAL
1000 GOSUB 1300  ' ADJUST ACES AFTER INITIAL DEAL (FIXES A+A SHOWING 22)
1010 REM PLAYER'S TURN - INITIAL DECISION
1020 GOSUB 4340  ' DISPLAY AND DESCRIBE PLAYER'S CARDS
1030 IF PLAYERTOTAL = 21 AND PLAYERCOUNT = 2 THEN PRINT "BLACKJACK! YOU WIN!": PRINT CHR$(7): PRINT "==> DEALER: BEGINNER'S LUCK!": BANKROLL = BANKROLL + INT(BET * 3 / 2): GOSUB 2180: RETURN
1040 IF PLAYERTOTAL = 21 THEN PRINT "YOU WIN!": PRINT CHR$(7): BANKROLL = BANKROLL + BET: GOSUB 2180: RETURN
1050 IF PLAYERTOTAL > 21 THEN GOSUB 1300: IF PLAYERTOTAL > 21 THEN RETURN
1060 GOSUB 4560
1070 GOSUB 1630: RETURN
1080 REM *** SUBROUTINE: PLAYER DOUBLES DOWN ***
1090 IF BET * 2 > BANKROLL THEN PRINT CHR$(7):PRINT "INSUFFICIENT FUNDS TO DOUBLE DOWN!": GOTO 1060
1100 TOTALDOUBLES = TOTALDOUBLES + 1
1110 TOTALMONEYBET = TOTALMONEYBET + BET
1120 BET = BET * 2  ' DOUBLE THE BET
1130 GOSUB 1400  ' DEAL ONE MORE CARD TO PLAYER AND HANDLE BUST
1140 IF PLAYERTOTAL > 21 THEN GOTO 1190  ' IF BUSTED, SKIP FINAL CARD MESSAGE
1150 PRINT "**************************************"
1160 C = PLAYERCARD(PLAYERCOUNT): GOSUB 5590: PRINT "YOUR FINAL CARD: "; D$
1170 GOSUB 4340  ' DISPLAY AND DESCRIBE PLAYER'S CARDS
1180 IF PLAYERTOTAL = 21 THEN PRINT CHR$(7): PRINT "YOU REACHED 21!": GOSUB 1630
1190 RETURN 
1200 REM *** SUBROUTINE: PLAYER SURRENDERS ***
1210 PRINT "YOU CHOSE TO SURRENDER. YOU FORFEIT HALF OF YOUR BET."
1220 PRINT CHR$(7): PRINT "==> DEALER: SOMETIMES IT'S WISE TO CUT YOUR LOSSES."
1230 BANKROLL = BANKROLL - INT(BET / 2)
1240 PLAYERSURRD = 1  ' SET PLAYER SURRENDERED FLAG
1250 PLAYERSURRS = PLAYERSURRS + 1  ' INCREMENT PLAYER SURRENDER COUNTER
1260 DEALERPLAYD = 1  ' INDICATE THAT DEALER'S TURN SHOULD BE SKIPPED
1270 PRINT "CURRENT BANKROLL: $"; BANKROLL
1280 GOSUB 2500  ' UPDATE LOSS STATS (SURRENDER COUNTS AS LOSS)
1290 RETURN
1300 REM *** SUBROUTINE: ADJUST ACES FOR PLAYER AND DEALER ***
1310 REM ADJUST ACES FOR PLAYER
1320 FOR I = 1 TO PLAYERCOUNT
1330 IF PLAYERVALUE(I) = 11 AND PLAYERTOTAL > 21 THEN PLAYERTOTAL = PLAYERTOTAL - 10: PLAYERVALUE(I) = 1
1340 NEXT I
1350 REM ADJUST ACES FOR DEALER
1360 FOR I = 1 TO DEALERCOUNT
1370 IF DEALERVALUE(I) = 11 AND DEALERTOTAL > 21 THEN DEALERTOTAL = DEALERTOTAL - 10: DEALERVALUE(I) = 1
1380 NEXT I
1390 RETURN
1400 REM *** SUBROUTINE: DEAL ANOTHER CARD TO PLAYER ***
1410 PLAYERCOUNT = PLAYERCOUNT + 1
1420 IF PLAYERCOUNT > 52 THEN PRINT "ERROR: TOO MANY PLAYER CARDS!": END
1430 IF NEXTCARD > 312 THEN PRINT "ERROR: OUT OF CARDS. RESHUFFLING...": GOSUB 4990
1440 PLAYERCARD(PLAYERCOUNT) = CARD(NEXTCARD)
1450 PLAYERVALUE(PLAYERCOUNT) = VALUE(PLAYERCARD(PLAYERCOUNT))
1460 NEXTCARD = NEXTCARD + 1
1470 PLAYERTOTAL = PLAYERTOTAL + PLAYERVALUE(PLAYERCOUNT)
1480 GOSUB 1300  ' ADJUST ACES AFTER ADDING CARD
1490 PRINT "**************************************"
1500 C = PLAYERCARD(PLAYERCOUNT): GOSUB 5590: PRINT "NEW CARD: "; D$
1510 GOSUB 4340  ' DISPLAY AND DESCRIBE PLAYER'S CARDS
1520 IF PLAYERTOTAL > 21 THEN GOTO 1540
1530 RETURN
1540 REM *** SUBROUTINE: PLAYER BUSTED MESSAGE ***
1550 PRINT CHR$(7): PRINT "BUSTED! YOU LOSE!"
1560 GOSUB 4670
1570 BANKROLL = BANKROLL - BET  ' DEDUCT THE BET AMOUNT
1580 IF BANKROLL < 0 THEN BANKROLL = 0
1590 GOSUB 2500  ' UPDATE LOSS STATISTICS
1600 PLAYERBUSTD = 1  ' SET PLAYER BUSTED FLAG
1610 FIRSTMOVE = 1  ' RESET FIRST MOVE FLAG
1620 RETURN
1630 REM *** SUBROUTINE: DEALER’S TURN ***
1640 IF PLAYERBUSTD = 1 THEN RETURN  ' SKIP DEALER'S TURN IF PLAYER BUSTED
1650 IF DEALERPLAYD = 1 THEN RETURN  ' SKIP DEALER'S TURN IF ALREADY PLAYED
1660 DEALERPLAYD = 1  ' SET FLAG INDICATING DEALER HAS PLAYED
1670 GOSUB 4910 ' DEALER MY TURN MESSAGE ROUTINE
1680 REM REVEAL THE DEALER'S HIDDEN CARD AND ADD TO TOTAL
1690 DEALERTOTAL = DEALERTOTAL + DEALERVALUE(2)
1700 GOSUB 4450  ' DISPLAY DEALER'S CARDS
1710 REM DEALER HITS IF TOTAL IS LESS THAN 17
1720 IF DEALERTOTAL > 17 THEN GOTO 1890
1730 IF DEALERTOTAL = 17 THEN GOSUB 5270: IF SOFT17 = 0 THEN GOTO 1890
1740 DEALERCOUNT = DEALERCOUNT + 1
1750 IF DEALERCOUNT > 52 THEN PRINT "ERROR: TOO MANY DEALER CARDS!": END
1760 IF NEXTCARD > 312 THEN PRINT "ERROR: OUT OF CARDS. RESHUFFLING...": GOSUB 4990
1770 DEALERCARD(DEALERCOUNT) = CARD(NEXTCARD)
1780 DEALERVALUE(DEALERCOUNT) = VALUE(DEALERCARD(DEALERCOUNT))
1790 NEXTCARD = NEXTCARD + 1
1800 DEALERTOTAL = DEALERTOTAL + DEALERVALUE(DEALERCOUNT)
1810 GOSUB 1300  ' ADJUST ACES AFTER ADDING CARD
1820 PRINT "**************************************"
1830 C = DEALERCARD(DEALERCOUNT): GOSUB 5590: PRINT "DEALER HITS: "; D$
1840 GOSUB 4450  ' DISPLAY DEALER'S CARDS
1850 IF DEALERTOTAL > 21 THEN PRINT "**************************************": PRINT "DEALER BUSTED! YOU WIN!": PRINT CHR$(7): BANKROLL = BANKROLL + BET: GOSUB 2180: RETURN
1860 GOTO 1720
1870 REM DEALER STANDS, CONTINUE THE GAME
1880 GOSUB 1300  ' Final Ace adjustment before evaluating bust or win
1890 REM CHECK IF DEALER BUSTED
1900 IF DEALERTOTAL > 21 THEN PRINT "DEALER BUSTED! YOU WIN!": PRINT CHR$(7): BANKROLL = BANKROLL + BET: GOSUB 2180: RETURN
1910 REM DETERMINE WINNER
1920 PRINT "**************************************"
1930 PRINT "DEALER STANDS WITH TOTAL: "; DEALERTOTAL
1940 PRINT "YOUR TOTAL: "; PLAYERTOTAL
1950 PRINT "**************************************"
1960 IF PLAYERTOTAL > DEALERTOTAL THEN PRINT "YOU WIN!": PRINT CHR$(7): BANKROLL = BANKROLL + BET: PRINT "CURRENT BANKROLL: $"; BANKROLL: GOSUB 2180: RETURN
1970 IF DEALERTOTAL > PLAYERTOTAL THEN PRINT "DEALER WINS!": GOSUB 4670: BANKROLL = BANKROLL - BET: PRINT "CURRENT BANKROLL: $"; BANKROLL: GOSUB 2500: RETURN
1980 PRINT "IT'S A TIE!": GOSUB 4750: GOSUB 2260: PRINT "CURRENT BANKROLL: $"; BANKROLL: RETURN
1990 PRINT "CURRENT BANKROLL: $"; BANKROLL
2000 GOSUB 2180
2010 FIRSTMOVE = 1
2020 RETURN
2030 REM *** SUBROUTINE: INITIALIZE STATISTICS ***
2040 GP = 0
2050 WN = 0
2060 TOTALLOSSES = 0
2070 TOTALBLACKJACKS = 0
2080 BS = 0
2090 HIGHBANKROLL = 1000
2100 TOTALMONEYBET = 0
2110 TOTALMONEYWON = 0
2120 TOTALMONEYLOST = 0
2130 TL = 0
2140 TOTALRESHUFFLES = 0
2150 TOTALCARDSDEALT = 0
2160 TOTALDOUBLES = 0
2170 RETURN  ' END OF INITIALIZE STATISTICS SUBROUTINE
2180 REM *** SUBROUTINE: UPDATE WIN STATISTICS ***
2190 GP = GP + 1  ' INCREMENT TOTAL GAMES PLAYED
2200 WN = WN + 1  ' INCREMENT TOTAL WINS
2210 IF PLAYERTOTAL = 21 AND PLAYERCOUNT = 2 THEN TOTALBLACKJACKS = TOTALBLACKJACKS + 1  ' CHECK FOR BLACKJACK
2220 IF PLAYERTOTAL = 21 AND PLAYERCOUNT = 2 THEN TOTALMONEYWON = TOTALMONEYWON + INT(BET * 3 / 2) ELSE TOTALMONEYWON = TOTALMONEYWON + BET
2230 IF BANKROLL > HIGHBANKROLL THEN HIGHBANKROLL = BANKROLL  ' UPDATE HIGHEST BANKROLL
2240 TOTALCARDSDEALT = TOTALCARDSDEALT + PLAYERCOUNT + DEALERCOUNT
2250 RETURN  ' END OF WIN STATISTICS SUBROUTINE
2260 REM *** SUBROUTINE: UPDATE TIE STATISTICS ***
2270 GP = GP + 1
2280 TL = TL + 1  ' INCREMENT TOTAL TIES
2290 TOTALCARDSDEALT = TOTALCARDSDEALT + PLAYERCOUNT + DEALERCOUNT
2300 RETURN  ' END OF TIE STATISTICS SUBROUTINE
2310 REM *** SUBROUTINE: DISPLAY TITLE SCREEN ***
2320 PRINT CHR$(12) ' CLEAR SCREEN
2330 PRINT "****************************************"
2340 PRINT "*          WELCOME TO BLACKJACK        *"
2350 PRINT "*                                      *"
2360 PRINT "*      CREATED BY CHRISTOPHER          *"
2370 PRINT "*           D. VAN DER KAAY            *"
2380 PRINT "*                                      *"
2390 PRINT "*   +----+  +----+  +----+  +----+     *"
2400 PRINT "*   | K  |  | Q  |  | J  |  | A  |     *"
2410 PRINT "*   | *  |  | #  |  | @  |  | %  |     *"
2420 PRINT "*   +----+  +----+  +----+  +----+     *"
2430 PRINT "*                                      *"
2440 PRINT "*     6-DECK SHOE  |  H17  |  3:2 BJ   *"
2450 PRINT "*                                      *"
2460 PRINT "*         PRESS RETURN TO START        *"
2470 PRINT "****************************************"
2480 PRINT CHR$(7);: INPUT ""; A$
2490 RETURN
2500 REM *** SUBROUTINE: UPDATE LOSS STATISTICS ***
2510 GP = GP + 1  ' INCREMENT TOTAL GAMES PLAYED
2520 TOTALLOSSES = TOTALLOSSES + 1  ' SURRENDER COUNTS AS A LOSS TOO
2530 IF PLAYERTOTAL > 21 THEN BS = BS + 1  ' CHECK FOR BUST
2540 TOTALMONEYLOST = TOTALMONEYLOST + BET  ' FULL BET LOST FOR NON-SURRENDER LOSSES
2550 IF PLAYERSURRD = 1 THEN TOTALMONEYLOST = TOTALMONEYLOST - INT(BET / 2)  ' ADJUST TO HALF-LOSS FOR SURRENDER
2560 TOTALCARDSDEALT = TOTALCARDSDEALT + PLAYERCOUNT + DEALERCOUNT
2570 PLAYERSURRD = 0  ' RESET PLAYER SURRENDERED FLAG FOR NEXT GAME
2580 RETURN
2590 REM *** SUBROUTINE: DISPLAY INSTRUCTIONS ***
2600 PRINT CHR$(12)  ' CLEAR SCREEN
2610 PRINT "DO YOU WANT TO READ THE INSTRUCTIONS? (Y/N) "; 
2620 INPUT A$
2630 GOSUB 3520  ' CONVERT INPUT TO UPPERCASE
2640 IF UPPERA$ = "Y" THEN GOTO 2680
2650 IF UPPERA$ = "N" THEN RETURN
2660 PRINT "INVALID INPUT. PLEASE ENTER Y OR N."
2670 GOTO 2620  ' ASK AGAIN
2680 PRINT CHR$(12)
2690 PRINT "===================="
2700 PRINT "   INSTRUCTIONS     "
2710 PRINT "===================="
2720 PRINT
2730 PRINT "1. OBJECTIVE:"
2740 PRINT "GET AS CLOSE TO 21 AS POSSIBLE WITHOUT EXCEEDING IT."
2750 PRINT
2760 PRINT "2. CARD VALUES:"
2770 PRINT "- NUMBER CARDS (2-10) ARE FACE VALUE."
2780 PRINT "- FACE CARDS (J, Q, K) ARE WORTH 10."
2790 PRINT "- ACES CAN BE 1 OR 11."
2800 PRINT
2810 PRINT "PRESS RETURN TO CONTINUE..."
2820 INPUT ""; A$
2830 PRINT "3. GAMEPLAY:"
2840 PRINT "- BOTH YOU AND THE DEALER START WITH TWO CARDS."
2850 PRINT "- ONE OF THE DEALER'S CARDS IS FACE UP."
2860 PRINT "- YOUR CARDS ARE FACE UP."
2870 PRINT "- THE GAME USES 6 DECKS OF CARDS."
2880 PRINT
2890 PRINT "4. YOUR TURN:"
2900 PRINT "- [H]IT: TAKE ANOTHER CARD."
2910 PRINT "- [S]TAND: END YOUR TURN."
2920 PRINT "- [D]OUBLE DOWN: DOUBLE YOUR BET, TAKE ONE FINAL CARD, AND END YOUR TURN."
2930 PRINT "- [R] SURRENDER: FORFEIT HALF YOUR BET AND END YOUR TURN."
2940 PRINT
2950 PRINT "PRESS RETURN TO CONTINUE..."
2960 INPUT ""; A$
2970 PRINT CHR$(12)
2980 PRINT "5. DEALER'S TURN:"
2990 PRINT "- DEALER HITS UNTIL THE TOTAL IS 17 OR MORE."
3000 PRINT "- DEALER STANDS ON HARD 17 OR HIGHER."
3010 PRINT "- DEALER HITS ON SOFT 17 (H17)."
3020 PRINT "- DEALER'S ACES ADJUST FROM 11 TO 1 IF NECESSARY TO AVOID BUSTING."
3030 PRINT
3040 PRINT "6. WINNING:"
3050 PRINT "- IF YOUR TOTAL IS CLOSER TO 21 THAN THE DEALER'S, YOU WIN."
3060 PRINT "- IF THE DEALER'S TOTAL IS CLOSER TO 21, THE DEALER WINS."
3070 PRINT "- A TIE RESULTS IN NO CHANGE TO THE BANKROLL."
3080 PRINT "- IF YOU BUST (TOTAL EXCEEDS 21), YOU LOSE IMMEDIATELY."
3090 PRINT
3100 PRINT "PRESS RETURN TO CONTINUE..."
3110 INPUT ""; A$
3120 PRINT CHR$(12)
3130 PRINT "7. SPECIAL RULES:"
3140 PRINT "- THE GAME USES 6 DECKS OF CARDS."
3150 PRINT "- NATURAL BLACKJACK (21 WITH THE FIRST TWO CARDS) PAYS 3:2."
3160 PRINT "  EXAMPLE: IF YOU BET $100 AND GET A NATURAL BLACKJACK, YOU WIN $150"
3170 PRINT "  PLUS YOUR ORIGINAL BET, FOR A TOTAL PAYOUT OF $250."
3180 PRINT "- INSURANCE: IF THE DEALER SHOWS AN ACE, YOU MAY PLACE AN"
3190 PRINT "  INSURANCE BET IS HALF YOUR BET (ROUNDED DOWN)."
3200 PRINT "  IF THE DEALER HAS BLACKJACK, INSURANCE PAYS 2 TO 1."
3210 PRINT "  IF NOT, YOU LOSE THE INSURANCE BET."
3220 PRINT "  EXAMPLE: IF YOU BET $100 AND PLACE A $50 INSURANCE BET,"
3230 PRINT "  YOU WIN $100 FROM INSURANCE IF THE DEALER HAS BLACKJACK."
3240 PRINT "  IF NOT, YOU LOSE THE $50 INSURANCE BET AND CONTINUE THE HAND."
3250 PRINT "- IF THE DEALER BUSTS (TOTAL EXCEEDS 21), YOU WIN."
3260 PRINT "  EXAMPLE: IF YOU BET $150 AND THE DEALER BUSTS, YOU WIN $150."
3270 PRINT
3280 PRINT "PRESS RETURN TO CONTINUE..."
3290 INPUT ""; A$ ' WAIT FOR PLAYER TO PRESS RETURN
3300 PRINT "- DOUBLE DOWN: YOU CAN DOUBLE YOUR BET, TAKE ONE FINAL CARD, AND END YOUR TURN."
3310 PRINT "  EXAMPLE: IF YOU BET $50 AND DOUBLE DOWN, YOU BET AN ADDITIONAL $50."
3320 PRINT "  IF YOU WIN WITH THIS HAND, YOUR TOTAL BET ($100) IS PAID OUT."
3330 PRINT "- SURRENDER: FORFEIT HALF YOUR BET AND END YOUR TURN."
3340 PRINT "  EXAMPLE: IF YOU BET $80 AND SURRENDER, YOU LOSE $40 (HALF YOUR BET)."
3350 PRINT "- IF THE DEALER HAS A NATURAL 21 (BLACKJACK) AND YOU DO NOT, YOU LOSE YOUR BET."
3360 PRINT "  EXAMPLE: IF YOU BET $100 AND THE DEALER HAS A NATURAL 21 WHILE YOU DO NOT,"
3370 PRINT "  YOU LOSE $100."
3380 PRINT "- IF BOTH YOU AND THE DEALER HAVE A NATURAL 21, IT IS A PUSH (TIE)."
3390 PRINT
3400 PRINT "PRESS RETURN TO CONTINUE..."
3410 INPUT ""; A$  ' WAIT FOR PLAYER TO PRESS RETURN
3420 PRINT "8. TIPS:"
3430 PRINT "- STAND IF YOUR TOTAL IS 12-16 AND DEALER SHOWS 2-6."
3440 PRINT "- HIT IF YOUR TOTAL IS 12-16 AND DEALER SHOWS 7 OR MORE."
3450 PRINT "- ALWAYS HIT IF YOUR TOTAL IS 11 OR LESS."
3460 PRINT "- DOUBLE DOWN WITH A TOTAL OF 10 OR 11 WHEN DEALER SHOWS LOWER THAN 10."
3470 PRINT "- SURRENDER IF THE SITUATION LOOKS UNFAVORABLE AND SAVE HALF YOUR BET."
3480 PRINT
3490 PRINT "PRESS RETURN TO CONTINUE..."
3500 INPUT ""; A$  ' WAIT FOR PLAYER TO PRESS RETURN
3510 RETURN  ' END OF INSTRUCTIONS SUBROUTINE
3520 REM *** SUBROUTINE: CONVERT INPUT TO UPPERCASE ***
3530 UPPERA$ = ""
3540 FOR I = 1 TO LEN(A$)
3550 A = ASC(MID$(A$, I, 1))  ' GET ASCII CODE OF CHARACTER
3560 IF A >= 97 THEN GOTO 3580
3570 GOTO 3600
3580 IF A <= 122 THEN A = A - 32
3590 GOTO 3620
3600 UPPERA$ = UPPERA$ + CHR$(A)
3610 GOTO 3630
3620 UPPERA$ = UPPERA$ + CHR$(A)
3630 NEXT I
3640 RETURN  ' END OF UPPERCASE CONVERSION SUBROUTINE
3650 REM *** SUBROUTINE: EXIT SCREEN ***
3660 PRINT CHR$(12) ' CLEAR SCREEN
3670 PRINT "CURRENT BANKROLL: $"; BANKROLL
3680 PRINT
3690 PRINT "*********************************"
3700 PRINT "*                               *"
3710 PRINT "*       THINK YOU'VE HAD        *"
3720 PRINT "*       ENOUGH FOR TODAY?       *"
3730 PRINT "*                               *"
3740 PRINT "*       THANKS FOR PLAYING      *"
3750 PRINT "*       BLACKJACK WITH ME!      *"
3760 PRINT "*                               *"
3770 PRINT "*********************************"
3780 PRINT "CREATED BY CHRISTOPHER D. VAN DER KAAY"
3790 PRINT
3800 PRINT "SEE YOU NEXT TIME—IF YOU DARE!"
3810 RETURN
3820 REM *** SUBROUTINE: BANKRUPT SCREEN ***
3830 PRINT CHR$(12) ' CLEAR SCREEN
3840 PRINT "CURRENT BANKROLL: $"; BANKROLL
3850 PRINT
3860 PRINT "*********************************"
3870 PRINT "*                               *"
3880 PRINT "*          BANKRUPT!            *"
3890 PRINT "*        GAME OVER!             *"
3900 PRINT "*                               *"
3910 PRINT "*********************************"
3920 PRINT "LOOKS LIKE YOU'RE OUT OF FUNDS. TOUGH LUCK!"
3930 PRINT "DON'T LET IT GET TO YOU. EVEN THE BEST HAVE BAD DAYS."
3940 PRINT "REMEMBER, BLACKJACK IS ALL ABOUT STRATEGY AND KEEPING YOUR COOL."
3950 PRINT "COME BACK AND TRY AGAIN. MAYBE NEXT TIME YOU'LL HAVE WHAT IT TAKES TO BEAT ME!"
3960 PRINT CHR$(7)
3970 GOSUB 3990  ' ASK TO VIEW STATISTICS
3980 RETURN
3990 REM *** SUBROUTINE: PROMPT TO VIEW STATISTICS ***
4000 INPUT "DO YOU WANT TO VIEW GAME STATISTICS? (Y/N) "; A$
4010 GOSUB 3520  ' CONVERT INPUT TO UPPERCASE
4020 IF UPPERA$ = "Y" THEN GOSUB 4060: RETURN
4030 IF UPPERA$ = "N" THEN RETURN
4040 PRINT "INVALID INPUT. PLEASE ENTER Y OR N."
4050 GOTO 4000  ' ASK AGAIN
4060 REM *** SUBROUTINE: DISPLAY GAME STATISTICS ***
4070 PRINT CHR$(12) ' CLEAR SCREEN
4080 PRINT "********************************************************************************"
4090 PRINT "                              PLAYER GAME STATISTICS"
4100 PRINT "********************************************************************************"
4110 PRINT "* GAMES PLAYED:     "; RIGHT$(SPACE$(5) + STR$(GP), 5); TAB(45); "BLACKJACKS:         "; RIGHT$(SPACE$(5) + STR$(TOTALBLACKJACKS), 5)
4120 PRINT "* WINS:             "; RIGHT$(SPACE$(5) + STR$(WN), 5); TAB(45); "BUSTS:              "; RIGHT$(SPACE$(5) + STR$(BS), 5)
4130 PRINT "* LOSING HANDS:     "; RIGHT$(SPACE$(5) + STR$(TOTALLOSSES), 5); TAB(45); "SURRENDERS:         "; RIGHT$(SPACE$(5) + STR$(PLAYERSURRS), 5)
4140 PRINT "* TIES:             "; RIGHT$(SPACE$(5) + STR$(TL), 5); TAB(45); "INS. BETS:          "; RIGHT$(SPACE$(5) + STR$(TOTALINSURANCES), 5)
4150 PRINT "* RESHUFFLES:       "; RIGHT$(SPACE$(5) + STR$(TOTALRESHUFFLES), 5); TAB(45); "INS. WINS:          "; RIGHT$(SPACE$(5) + STR$(INSURANCEWINS), 5)
4160 IF GP > 0 THEN AVERAGECARDSDEALT = INT(TOTALCARDSDEALT / GP) ELSE AVERAGECARDSDEALT = 0
4170 PRINT "* AVG CARDS/GAME:   "; RIGHT$(SPACE$(5) + STR$(AVERAGECARDSDEALT), 5); TAB(45); "DOUBLES:            "; RIGHT$(SPACE$(5) + STR$(TOTALDOUBLES), 5)
4180 PRINT "********************************************************************************"
4190 PRINT "                                FINANCIAL STATS"
4200 PRINT "********************************************************************************"
4210 PRINT "* HIGH BANKROLL:    $"; RIGHT$(SPACE$(6) + STR$(HIGHBANKROLL), 6); TAB(45); "MONEY BET:          $"; RIGHT$(SPACE$(6) + STR$(TOTALMONEYBET), 6)
4220 PRINT "* MONEY WON:        $"; RIGHT$(SPACE$(6) + STR$(TOTALMONEYWON), 6); TAB(45); "MONEY LOST:         $"; RIGHT$(SPACE$(6) + STR$(TOTALMONEYLOST), 6)
4230 PRINT "********************************************************************************"
4240 PRINT "PRESS RETURN TO CONTINUE..."
4250 INPUT ""; A$
4260 RETURN  ' END OF STATISTICS DISPLAY SUBROUTINE
4270 REM *** SUBROUTINE: VALIDATE BET INPUT ***
4280 VALIDBET = 1  ' ASSUME INPUT IS VALID INITIALLY
4290 FOR I = 1 TO LEN(BET$)
4300 CHAR$ = MID$(BET$, I, 1)
4310 IF CHAR$ < "0" OR CHAR$ > "9" THEN VALIDBET = 0: GOTO 4330
4320 NEXT I
4330 RETURN
4340 REM *** SUBROUTINE: DISPLAY PLAYER’S CARDS ***
4350 PRINT "**************************************"
4360 PRINT "YOUR CARDS: ";
4370 FOR I = 1 TO PLAYERCOUNT
4380 C = PLAYERCARD(I): GOSUB 5590
4390 PRINT D$;
4400 PRINT " ";
4410 NEXT I
4420 PRINT "    TOTAL: "; PLAYERTOTAL
4430 PRINT "**************************************"
4440 RETURN
4450 REM *** SUBROUTINE: DISPLAY DEALER’S CARDS ***
4460 PRINT "**************************************"
4470 PRINT "DEALER'S CARDS: ";
4480 FOR I = 1 TO DEALERCOUNT
4490 C = DEALERCARD(I): GOSUB 5590
4500 PRINT D$;
4510 PRINT " ";
4520 NEXT I
4530 PRINT "    TOTAL: "; DEALERTOTAL
4540 PRINT "**************************************"
4550 RETURN
4560 REM *** SUBROUTINE: PLAYER’S DECISION HANDLER ***
4570 C = DEALERCARD(1): GOSUB 5590: PRINT "DEALER'S VISIBLE CARD: "; D$
4580 PRINT:PRINT "DO YOU WANT TO [H]IT, [S]TAND, [D]OUBLE DOWN, [R] SURRENDER? ";
4590 INPUT A$
4600 GOSUB 3520  ' CONVERT INPUT TO UPPERCASE
4610 IF FIRSTMOVE = 0 AND (UPPERA$ = "D" OR UPPERA$ = "R") THEN PRINT CHR$(7): PRINT "==> DEALER: YOU CAN ONLY PERFORM THIS ACTION ON YOUR FIRST MOVE!": GOTO 4580
4620 IF UPPERA$ <> "H" AND UPPERA$ <> "S" AND UPPERA$ <> "D" AND UPPERA$ <> "R" THEN PRINT CHR$(7): PRINT "==> DEALER: INVALID INPUT. PLEASE ENTER H, S, D, OR R.": GOTO 4580
4630 IF UPPERA$ = "H" THEN GOSUB 1400: FIRSTMOVE = 0: IF PLAYERTOTAL > 21 THEN RETURN ELSE GOTO 4560
4640 IF UPPERA$ = "S" THEN PRINT CHR$(7): PRINT "==> DEALER: PLAYING IT SAFE, HUH?": FIRSTMOVE = 0: GOSUB 1630: RETURN
4650 IF UPPERA$ = "D" THEN GOSUB 1080: FIRSTMOVE = 0: RETURN
4660 IF UPPERA$ = "R" THEN GOSUB 1200: FIRSTMOVE = 0: RETURN
4670 REM *** SUBROUTINE: DEALER WIN MESSAGES **
4680 MESSAGENUMB = INT(RND * 5) + 1
4690 ON MESSAGENUMB GOTO 4700, 4710, 4720, 4730, 4740
4700 PRINT CHR$(7): PRINT "==> DEALER: BETTER LUCK NEXT TIME!": RETURN
4710 PRINT CHR$(7): PRINT "==> DEALER: YOU CAN'T WIN THEM ALL!": RETURN
4720 PRINT CHR$(7): PRINT "==> DEALER: CLOSE, BUT NO CIGAR!": RETURN
4730 PRINT CHR$(7): PRINT "==> DEALER: MAYBE NEXT TIME!": RETURN
4740 PRINT CHR$(7): PRINT "==> DEALER: THE HOUSE ALWAYS WINS!": RETURN
4750 REM *** SUBROUTINE: DEALER TIE MESSAGES ***
4760 MESSAGENUMB = INT(RND * 5) + 1
4770 ON MESSAGENUMB GOTO 4780, 4790, 4800, 4810, 4820
4780 PRINT CHR$(7): PRINT "==> DEALER: A TIE? HOW BORING!": RETURN
4790 PRINT CHR$(7): PRINT "==> DEALER: NO WINNER THIS TIME!": RETURN
4800 PRINT CHR$(7): PRINT "==> DEALER: WELL, THAT WAS UNEXPECTED!": RETURN
4810 PRINT CHR$(7): PRINT "==> DEALER: A DRAW? THAT'S RARE!": RETURN
4820 PRINT CHR$(7): PRINT "==> DEALER: A STALEMATE! INTERESTING!": RETURN
4830 REM *** SUBROUTINE: DEALER OPENING MESSAGES ***
4840 MESSAGENUMB = INT(RND * 5) + 1
4850 ON MESSAGENUMB GOTO 4860, 4870, 4880, 4890, 4900
4860 PRINT CHR$(7): PRINT "==> DEALER: LET'S SEE IF YOU CAN BEAT ME TODAY!": RETURN
4870 PRINT CHR$(7): PRINT "==> DEALER: LET'S PLAY. BUT DON'T GET YOUR HOPES UP TOO HIGH!": RETURN
4880 PRINT CHR$(7): PRINT "==> DEALER: THINK YOU CAN OUTSMART ME? LET'S SEE WHAT YOU'VE GOT!": RETURN
4890 PRINT CHR$(7): PRINT "==> DEALER: I'VE SEEN BETTER PLAYERS. PROVE ME WRONG!": RETURN
4900 PRINT CHR$(7): PRINT "==> DEALER: READY TO LOSE AGAIN? BRING IT ON!": RETURN
4910 REM *** SUBROUTINE: DEALER MY TURN MESSAGES ***
4920 MESSAGENUMB = INT(RND * 5) + 1
4930 ON MESSAGENUMB GOTO 4940, 4950, 4960, 4970, 4980
4940 PRINT CHR$(7): PRINT "==> DEALER: ALRIGHT, MY TURN!": RETURN
4950 PRINT CHR$(7): PRINT "==> DEALER: LET ME TAKE OVER FROM HERE!": RETURN
4960 PRINT CHR$(7): PRINT "==> DEALER: NOW WATCH A PRO IN ACTION!": RETURN
4970 PRINT CHR$(7): PRINT "==> DEALER: STEP ASIDE, IT'S MY TURN!": RETURN
4980 PRINT CHR$(7): PRINT "==> DEALER: TIME FOR ME TO TAKE THE SPOTLIGHT!": RETURN
4990 REM *** SUBROUTINE: SHUFFLE AND INITIALIZE DECK ***
5000 FOR I = 1 TO 312
5010 CARD(I) = I
5020 VALUE(I) = (I - 1) MOD 13 + 1
5030 IF VALUE(I) > 10 THEN VALUE(I) = 10
5040 IF VALUE(I) = 1 THEN VALUE(I) = 11
5050 NEXT I
5060 FOR I = 312 TO 2 STEP -1
5070 J = INT(RND * I) + 1
5080 IF I = J THEN GOTO 5100
5090 T = CARD(I): CARD(I) = CARD(J): CARD(J) = T
5100 NEXT I
5110 NEXTCARD = 1
5120 TOTALRESHUFFLES = TOTALRESHUFFLES + 1
5130 RETURN
5140 REM *** SUBROUTINE: END OF GAME AND CREDITS ***
5150 PRINT CHR$(12) ' CLEAR SCREEN
5160 PRINT "*************************************"
5170 PRINT "*                                   *"
5180 PRINT "*        THANK YOU FOR PLAYING      *"
5190 PRINT "*                                   *"
5200 PRINT "*            BLACKJACK!             *"
5210 PRINT "*                                   *"
5220 PRINT "*      CREATED BY CHRISTOPHER       *"
5230 PRINT "*         D. VAN DER KAAY           *"
5240 PRINT "*                                   *"
5250 PRINT "*************************************"
5260 END
5270 REM *** SUBROUTINE: CHECK FOR SOFT 17 ***
5280 SOFT17 = 0
5290 IF DEALERTOTAL <> 17 THEN RETURN
5300 FOR I = 1 TO DEALERCOUNT
5310 IF DEALERVALUE(I) = 11 THEN SOFT17 = 1: RETURN
5320 NEXT I
5330 RETURN
5340 REM *** SUBROUTINE: OFFER INSURANCE ***
5350 PRINT "DEALER SHOWS AN ACE. DO YOU WANT TO BUY INSURANCE? (Y/N)";
5360 INPUT A$
5370 GOSUB 3520  ' CONVERT TO UPPERCASE
5380 IF UPPERA$ = "Y" THEN GOTO 5410
5390 IF UPPERA$ = "N" THEN RETURN
5400 PRINT "INVALID INPUT. PLEASE ENTER Y OR N.": GOTO 5350
5410 INSURANCEPLACED = 1
5420 INSURANCEBET = INT(BET / 2)
5430 IF INSURANCEBET < 1 THEN PRINT "INSURANCE BET IS $0.": INSURANCEPLACED = 0: INSURANCEBET = 0: RETURN
5440 IF INSURANCEBET > BANKROLL THEN PRINT CHR$(7): PRINT "NOT ENOUGH FUNDS FOR INSURANCE.": INSURANCEPLACED = 0: INSURANCEBET = 0: RETURN
5450 BANKROLL = BANKROLL - INSURANCEBET
5460 PRINT "YOU PLACED AN INSURANCE BET OF $"; INSURANCEBET
5470 PRINT "CURRENT BANKROLL: $"; BANKROLL
5480 TOTALINSURANCES = TOTALINSURANCES + 1
5490 RETURN
5500 REM *** SUBROUTINE: HANDLE DEALER BLACKJACK OUTCOME ***
5510 IF INSURANCEPLACED = 1 THEN PRINT "INSURANCE PAYS 2:1. PROFIT $"; INSURANCEBET * 2
5520 IF INSURANCEPLACED = 1 THEN BANKROLL = BANKROLL + INSURANCEBET * 3
5530 IF INSURANCEPLACED = 1 THEN INSURANCEWINS = INSURANCEWINS + 1
5540 PRINT "DEALER HAS BLACKJACK! YOU LOSE!": PRINT CHR$(7)
5550 BANKROLL = BANKROLL - BET
5560 GOSUB 2500
5570 INSURANCEPLACED = 0: INSURANCEBET = 0
5580 RETURN
5590 REM *** SUBROUTINE: CARD DISPLAY (RETURNS D$ FOR CARD ID IN C) ***
5600 IF C < 1 OR C > 312 THEN D$ = "??": RETURN
5610 R = (C - 1) MOD 13 + 1
5620 D$ = DISP$(R)
5630 RETURN
