10 REM *** V1.1 PROGRAMMED BY C. VAN DER KAAY 12/18/2025 ***
20 REM *** COMPUTER AI NOTES ***
30 REM *** EASY: COMPUTER CHOOSES ROCK/PAPER/SCISSORS AT RANDOM ***
40 REM *** MEDIUM: ANALYZES LAST 5 PLAYER MOVES AND BIASES TOWARD THE COUNTER MOVE ***
50 REM *** HARD: ANALYZES LAST 10 PLAYER MOVES WITH STRONGER COUNTER-MOVE BIAS ***
60 RANDOMIZE
70 DIM UC$(100), CC$(100)
80 GOSUB 460 ' DISPLAY INSTRUCTIONS
90 INPUT "CHOOSE DIFFICULTY (1=EASY, 2=MEDIUM, 3=HARD): ", DL
100 IF DL < 1 OR DL > 3 THEN PRINT "INVALID CHOICE! TRY AGAIN.": GOTO 90
110 GOSUB 1900 ' INITIALIZE GAME STATE
120 INPUT "HOW MANY ROUNDS WOULD YOU LIKE TO PLAY? ", TR
130 IF TR <= 0 THEN PRINT "PLEASE ENTER A POSITIVE NUMBER.": GOTO 120
140 IF TR > 100 THEN PRINT "MAXIMUM ROUNDS IS 100.": GOTO 120
150 US = 0
160 CS = 0
170 PW = 0
180 CW = 0
190 RC = 1
200 IF RC > TR THEN GOTO 310
210 IF INT(RND * 10) = 0 THEN PRINT "*** BONUS ROUND! POINTS ARE DOUBLED! ***": BR = 1: GOTO 230
220 BR = 0
230 REM CONTINUE
240 GOSUB 620
250 IF PC$ = "X" THEN GOSUB 2020: GOTO 240
260 GOSUB 740
270 GOSUB 1580
280 GOSUB 1740
290 RC = RC + 1
300 GOTO 200
310 GOSUB 1790 ' END GAME MESSAGE
320 INPUT "WOULD YOU LIKE TO VIEW THE STATISTICS (Y/N)? ", STATS$
330 STATS$ = LEFT$(STATS$,1)
340 A = ASC(STATS$)
350 IF A >= 97 THEN IF A <= 122 THEN STATS$ = CHR$(A-32)
360 IF STATS$ = "Y" THEN GOSUB 2020: GOTO 380
370 IF STATS$ <> "N" THEN PRINT "PLEASE ENTER Y OR N.": GOTO 320
380 INPUT "PLAY AGAIN (Y/N)? ", AGAIN$
390 AGAIN$ = LEFT$(AGAIN$,1)
400 A = ASC(AGAIN$)
410 IF A >= 97 THEN IF A <= 122 THEN AGAIN$ = CHR$(A-32)
420 IF AGAIN$ = "Y" THEN GOSUB 1900: GOTO 90
430 IF AGAIN$ <> "N" THEN PRINT "PLEASE ENTER Y OR N.": GOTO 380
440 PRINT : PRINT "GOOD BYE!"
450 END
460 REM *** SUBROUTINE TO DISPLAY INSTRUCTIONS ***
470 PRINT "*********************************************"
480 PRINT "*                                           *"
490 PRINT "*   WELCOME TO ROCK-PAPER-SCISSORS!         *"
500 PRINT "*                                           *"
510 PRINT "*  INSTRUCTIONS:                            *"
520 PRINT "*  CHOOSE ROCK (R), PAPER (P), OR SCISSORS  *"
530 PRINT "*  (S). THE GAME FOLLOWS THE STANDARD RULES:*"
540 PRINT "*  ROCK BEATS SCISSORS, SCISSORS BEAT PAPER,*"
550 PRINT "*  AND PAPER BEATS ROCK.                    *"
560 PRINT "*                                           *"
570 PRINT "*  THE PLAYER WITH THE MOST POINTS AFTER    *"
580 PRINT "*  THE SPECIFIED ROUNDS WINS.               *"
590 PRINT "*                                           *"
600 PRINT "*********************************************"
610 RETURN
620 REM *** SUBROUTINE TO GET PLAYER CHOICE ***
630 PRINT
640 PRINT "CHOOSE ROCK (R), PAPER (P), SCISSORS (S) OR STATS (X)."
650 PRINT "YOUR CHOICE (R/P/S/X): ";
660 INPUT PC$
670 PC$ = LEFT$(PC$, 1)
680 A = ASC(PC$)
690 IF A >= 97 THEN IF A <= 122 THEN PC$ = CHR$(A - 32)
700 IF PC$ <> "R" AND PC$ <> "P" AND PC$ <> "S" AND PC$ <> "X" THEN PRINT "INVALID CHOICE! TRY AGAIN.": GOTO 650
710 IF PC$ = "X" THEN RETURN
720 UC$(RC) = PC$
730 RETURN
740 REM *** SUBROUTINE TO GET COMPUTER CHOICE ***
750 IF DL = 1 THEN CP$ = MID$("RPS", INT(RND * 3) + 1, 1): GOTO 780
760 IF DL = 2 THEN GOSUB 810: GOTO 780
770 IF DL = 3 THEN GOSUB 1220
780 PRINT "COMPUTER CHOSE: "; CP$
790 CC$(RC) = CP$
800 RETURN
810 REM *** SUBROUTINE TO USE BASIC PATTERN RECOGNITION FOR MEDIUM LEVEL ***
820 IF RC > 5 THEN GOSUB 850: RETURN
830 CP$ = MID$("RPS", INT(RND * 3) + 1, 1)
840 RETURN
850 REM *** ANALYZE LAST 5 PLAYER CHOICES FOR PATTERN RECOGNITION ***
860 REM *** INITIALIZE COUNTS FOR EACH CHOICE ***
870 ROCKCOUNT = 0: PAPERCOUNT = 0: SCISSORSCOUNT = 0
880 REM *** LOOP THROUGH THE LAST 5 PLAYER CHOICES AND COUNT EACH TYPE ***
890 FOR I = RC - 5 TO RC - 1
900 IF I > 0 THEN GOTO 910 ELSE GOTO 950
910 IF UC$(I) = "R" THEN ROCKCOUNT = ROCKCOUNT + 1 ELSE GOTO 930
920 GOTO 950
930 IF UC$(I) = "P" THEN PAPERCOUNT = PAPERCOUNT + 1 ELSE GOTO 940
940 IF UC$(I) = "S" THEN SCISSORSCOUNT = SCISSORSCOUNT + 1
950 NEXT I
960 REM *** DETERMINE THE MOST FREQUENT PLAYER CHOICE IN THE LAST 5 ROUNDS ***
970 REM *** SET DEFAULT TO ROCK FOR COMPARISON PURPOSES ***
980 MOSTFREQUENTCHOICE$ = "R"
990 IF PAPERCOUNT > ROCKCOUNT AND PAPERCOUNT > SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = "P"
1000 IF SCISSORSCOUNT > ROCKCOUNT AND SCISSORSCOUNT > PAPERCOUNT THEN MOSTFREQUENTCHOICE$ = "S"
1010 REM *** HANDLE TIES BY RANDOM SELECTION AMONG TIED CHOICES ***
1020 IF ROCKCOUNT = PAPERCOUNT AND ROCKCOUNT > SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RP", INT(RND * 2) + 1, 1)
1030 IF ROCKCOUNT = SCISSORSCOUNT AND ROCKCOUNT > PAPERCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RS", INT(RND * 2) + 1, 1)
1040 IF PAPERCOUNT = SCISSORSCOUNT AND PAPERCOUNT > ROCKCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("PS", INT(RND * 2) + 1, 1)
1050 IF ROCKCOUNT = PAPERCOUNT AND ROCKCOUNT = SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RPS", INT(RND * 3) + 1, 1)
1060 REM *** CHOOSE COMPUTER MOVE (MEDIUM): BIASED COUNTER (60% WIN, 25% TIE, 15% LOSE) ***
1070 RANDNUM = INT(RND * 100)  '0-99
1080 IF MOSTFREQUENTCHOICE$ = "R" THEN GOTO 1120
1090 IF MOSTFREQUENTCHOICE$ = "P" THEN GOTO 1150
1100 IF MOSTFREQUENTCHOICE$ = "S" THEN GOTO 1180
1110 CP$ = MID$("RPS", INT(RND * 3) + 1, 1): RETURN
1120 IF RANDNUM < 60 THEN CP$ = "P": RETURN
1130 IF RANDNUM < 85 THEN CP$ = "R": RETURN
1140 CP$ = "S": RETURN
1150 IF RANDNUM < 60 THEN CP$ = "S": RETURN
1160 IF RANDNUM < 85 THEN CP$ = "P": RETURN
1170 CP$ = "R": RETURN
1180 IF RANDNUM < 60 THEN CP$ = "R": RETURN
1190 IF RANDNUM < 85 THEN CP$ = "S": RETURN
1200 CP$ = "P": RETURN
1210 RETURN
1220 REM *** ADVANCED PATTERN RECOGNITION FOR HARD LEVEL ***
1230 REM *** INITIALIZE COUNTS FOR EACH CHOICE ***
1240 ROCKCOUNT = 0: PAPERCOUNT = 0: SCISSORSCOUNT = 0
1250 REM *** LOOP THROUGH THE LAST 10 PLAYER CHOICES AND COUNT EACH TYPE ***
1260 FOR I = RC - 10 TO RC - 1
1270 IF I > 0 THEN GOTO 1280 ELSE GOTO 1320
1280 IF UC$(I) = "R" THEN ROCKCOUNT = ROCKCOUNT + 1 ELSE GOTO 1300
1290 GOTO 1320
1300 IF UC$(I) = "P" THEN PAPERCOUNT = PAPERCOUNT + 1 ELSE GOTO 1310
1310 IF UC$(I) = "S" THEN SCISSORSCOUNT = SCISSORSCOUNT + 1
1320 NEXT I
1330 REM *** DETERMINE THE MOST FREQUENT PLAYER CHOICE IN THE LAST 10 ROUNDS ***
1340 MOSTFREQUENTCHOICE$ = "R"
1350 IF PAPERCOUNT > ROCKCOUNT AND PAPERCOUNT > SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = "P"
1360 IF SCISSORSCOUNT > ROCKCOUNT AND SCISSORSCOUNT > PAPERCOUNT THEN MOSTFREQUENTCHOICE$ = "S"
1370 REM *** HANDLE TIES BY RANDOM SELECTION AMONG TIED CHOICES ***
1380 IF ROCKCOUNT = PAPERCOUNT AND ROCKCOUNT > SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RP", INT(RND * 2) + 1, 1)
1390 IF ROCKCOUNT = SCISSORSCOUNT AND ROCKCOUNT > PAPERCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RS", INT(RND * 2) + 1, 1)
1400 IF PAPERCOUNT = SCISSORSCOUNT AND PAPERCOUNT > ROCKCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("PS", INT(RND * 2) + 1, 1)
1410 IF ROCKCOUNT = PAPERCOUNT AND ROCKCOUNT = SCISSORSCOUNT THEN MOSTFREQUENTCHOICE$ = MID$("RPS", INT(RND * 3) + 1, 1)
1420 REM *** CHOOSE COMPUTER MOVE (HARD): BIASED COUNTER (75% WIN, 20% TIE, 5% LOSE) ***
1430 RANDNUM = INT(RND * 100)  '0-99
1440 IF MOSTFREQUENTCHOICE$ <> "R" THEN GOTO 1480
1450 IF RANDNUM < 75 THEN CP$ = "P": RETURN
1460 IF RANDNUM < 95 THEN CP$ = "R": RETURN
1470 CP$ = "S": RETURN
1480 IF MOSTFREQUENTCHOICE$ <> "P" THEN GOTO 1520
1490 IF RANDNUM < 75 THEN CP$ = "S": RETURN
1500 IF RANDNUM < 95 THEN CP$ = "P": RETURN
1510 CP$ = "R": RETURN
1520 IF MOSTFREQUENTCHOICE$ <> "S" THEN GOTO 1560
1530 IF RANDNUM < 75 THEN CP$ = "R": RETURN
1540 IF RANDNUM < 95 THEN CP$ = "S": RETURN
1550 CP$ = "P": RETURN
1560 CP$ = MID$("RPS", INT(RND * 3) + 1, 1)
1570 RETURN
1580 REM *** SUBROUTINE TO DETERMINE WINNER AND UPDATE SCORE ***
1590 IF PC$ = CP$ THEN PRINT : PRINT "TIE! SCORES REMAIN THE SAME.": TC = TC + 1: RETURN
1600 IF (PC$ = "R" AND CP$ = "S") OR (PC$ = "P" AND CP$ = "R") OR (PC$ = "S" AND CP$ = "P") THEN GOSUB 1620: RETURN
1610 GOSUB 1680: RETURN
1620 REM *** PLAYER WINS ***
1630 PW = PW + 1
1640 PRINT CHR$(7): PRINT : PRINT "*** YOU WIN! ***"
1650 IF BR = 1 THEN US = US + 2: RETURN
1660 US = US + 1
1670 RETURN
1680 REM *** COMPUTER WINS ***
1690 CW = CW + 1
1700 PRINT : PRINT "<<< COMPUTER WINS! >>>"
1710 IF BR = 1 THEN CS = CS + 2: RETURN
1720 CS = CS + 1
1730 RETURN
1740 REM *** SUBROUTINE TO DISPLAY SCORES ***
1750 PRINT : PRINT "YOUR SCORE: "; US
1760 PRINT "COMPUTER SCORE: "; CS
1770 PRINT "ROUNDS PLAYED: "; RC
1780 RETURN
1790 REM *** SUBROUTINE TO DISPLAY END GAME MESSAGE ***
1800 PRINT : PRINT "GAME OVER!"
1810 IF US > CS THEN PRINT "*** CONGRATULATIONS, YOU WON THE GAME! ***"
1820 IF CS > US THEN PRINT "<<< SORRY, THE COMPUTER WON THE GAME. >>>"
1830 IF US = CS THEN PRINT "=== THE GAME IS A TIE. ==="
1840 PRINT : PRINT "FINAL SCORES:"
1850 PRINT "  YOUR SCORE: "; US
1860 PRINT "  COMPUTER SCORE: "; CS
1870 PRINT "  TOTAL TIES: "; TC
1880 PRINT
1890 RETURN
1900 REM *** SUBROUTINE TO INITIALIZE GAME STATE ***
1910 US = 0
1920 CS = 0
1930 PW = 0
1940 CW = 0
1950 RC = 1
1960 TC = 0
1970 FOR I = 1 TO 100
1980 UC$(I) = ""
1990 CC$(I) = ""
2000 NEXT I
2010 RETURN
2020 REM *** SUBROUTINE TO DISPLAY GAME STATISTICS ***
2030 PRINT:PRINT "*********************************************"
2040 PRINT "*                GAME STATISTICS            *"
2050 PRINT "*********************************************"
2060 PRINT "* TOTAL ROUNDS PLAYED: "; RC - 1
2070 PRINT "* TOTAL TIES: "; TC
2080 PRINT "* TOTAL PLAYER WINS: "; PW
2090 PRINT "* TOTAL COMPUTER WINS: "; CW
2100 PRINT "* TOTAL PLAYER POINTS: "; US
2110 PRINT "* TOTAL COMPUTER POINTS: "; CS
2120 UR = 0: UP = 0: US2 = 0
2130 CR = 0: CP2 = 0: CS2 = 0
2140 FOR I = 1 TO RC - 1
2150 IF UC$(I) = "R" THEN UR = UR + 1
2160 IF UC$(I) = "P" THEN UP = UP + 1
2170 IF UC$(I) = "S" THEN US2 = US2 + 1
2180 IF CC$(I) = "R" THEN CR = CR + 1
2190 IF CC$(I) = "P" THEN CP2 = CP2 + 1
2200 IF CC$(I) = "S" THEN CS2 = CS2 + 1
2210 NEXT I
2220 PRINT "* PLAYER CHOICES: ROCK = "; UR; ", PAPER = "; UP; ", SCISSORS = "; US2
2230 PRINT "* COMPUTER CHOICES: ROCK = "; CR; ", PAPER = "; CP2; ", SCISSORS = "; CS2
2240 PRINT "*********************************************"
2250 RETURN
